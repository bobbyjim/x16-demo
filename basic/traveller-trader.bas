1 ? " *************************************"
2 ? " *                                   *"
3 ? " *     TRAVELLER TRADER 2020.05      *"
4 ? " *                                   *"
5 ? " *  - PILOT A MARAVA FREE TRADER     *"
6 ? " *  - BUY AND SELL CARGO             *"
7 ? " *  - EXPLORE THE SPINWARD MARCHES   *"
8 ? " *  - UPGRADE TO A NEW SHIP          *"
9 ? " *  - HUNT DOWN PIRATES              *"
10 ? " *                                   *"
11 ? " *************************************"
12 ?
13 REM INITIALIZE GAME
14 GOSUB 150
15 F = FRE(1)
16 GOSUB 129
17 UL$="------------------------------"
18 MV%=0
19 ?:? "FREE MEMORY: " F "BYTES"
20 ?
21 CR = 100000000 :REM TESTING ONLY
22 REM MAIN LOOP
23 GOSUB 85
24 ? "MENU" : ? UL$
25 ? " (B)YE"
26 IF LEFT$(UW$,1) = "A" THEN ? " (L) SHIP CATALOG"
27 ? " (M)AP"
28 ? " (T)RADE"
29 ? " (####) JUMP TO HEX"
30 IN$ = "" : INPUT IN$
31 IF IN$ = "B" THEN GOTO 39
32 IF IN$ = "L" THEN GOSUB 91
33 IF IN$ = "M" THEN GOSUB 121
34 IF IN$ = "T" THEN GOSUB 132
35 IF IN$ = "" GOTO 23
36 IF VAL(IN$) > 0 THEN GOSUB 67
37 GOTO 23
38 REM BYE AND SHOW SCORE
39 ? " *************************************"
40 ? " *                                   *"
41 ? " *      THANK YOU FOR PLAYING        *"
42 ? " *        TRAVELLER TRADER!          *"
43 ? " *                                   *"
44 ? " *************************************"
45 ?:? "YOUR STATISTICS:" :? UL$
46 ? " - JUMPS:    " MV%
47 ? " - CREDITS:  " CR "
48 IF MV%>0 THEN ? " - CR/JUMP:  " INT(CR/MV%)
49 ?
50 ? "PROGRAM WRITTEN BY ROBERT EAGLESTONE."
51 ?
52 ? "THE TRAVELLER GAME IN ALL FORMS IS "
53 ? " - OWNED BY FAR FUTURE ENTERPRISES. "
54 ? " - COPYRIGHT 1977 - 2020 FAR FUTURE "
55 ? "   ENTERPRISES. "
56 ? " - A REGISTERED TRADEMARK OF FAR"
57 ? "   FUTURE ENTERPRISES. "
58 ? " THIS PROGRAM IS FOR PERSONAL,"
59 ? "   NON-COMMERCIAL USE ONLY. "
60 ? " ANY USE OF FAR FUTURE ENTERPRISES"
61 ? "   COPYRIGHTED MATERIAL OR TRADEMARKS "
62 ? "   ANYWHERE IN THIS PROGRAM SHOULD NOT"
63 ? "   BE VIEWED AS A CHALLENGE TO THOSE"
64 ? "   COPYRIGHTS OR TRADEMARKS."
65 END
66 REM DO A JUMP
67 ?:? "COMPUTING JUMP TO HEX " IN$
68 C1 = VAL(LEFT$(IN$,2))       :REM DESTINATION COL
69 R1 = VAL(RIGHT$(IN$,2))      :REM DESTINATION ROW
70 JR  = SH(0,6)                :REM SHIP JUMP RATING
71 GOSUB 560    :REM POPULATES D% = DISTANCE
72 GOSUB 593 :REM OK GO
73 GOSUB 499
74 SY$=SY$(C,R)
75 GOSUB 522
76 FOR TT=0 TO 2000:NEXT
77 ?:? " - ENGAGING JUMP DRIVE."
78 FOR TT=0 TO 1000:NEXT
79 ?:? " - IN JUMPSPACE. PLEASE STAND BY..."
80 F = FRE(1)
81 IF IN$ = LO$ THEN MV%=MV%+1:?:? "WELCOME TO " NA$ : GOSUB 129
82 IF IN$ <> LO$ THEN ?:? "JUMP FAILURE"
83 RETURN
84 REM CURRENT STATUS
85 SY$ = SY$(C,R)
86 ?:? "BALANCE: CR" CR
87 GOSUB 522
88 ?:? "(" NA$ " " UW$ ") "; 
89 RETURN
90 REM SHOW SHIP CATALOG
91 ?
92 ? "     QSP    CLASS       CARGO  MCR"
93 ? "----------------------------------"
94 BS%=0
95 FOR N=0 TO 20
96 QS$=QS$(N) :IF QS$ = "" GOTO 107
97 PR%=QC%(N)
98 IF INT(CR/1000000) > PR% THEN BS% = 1
99 Q1$=LEFT$(QS$,6)  :REM QSP
100 Q3$=MID$(QS$,8,3) :REM CARGO
101 Q4$=MID$(QS$,12)  :REM CLASS
102 Q4$ = LEFT$(Q4$+"          ",12)
103 ? N; :IF N<10 THEN ? " ";
104 ? " " Q1$ " " Q4$ " " Q3$ "T " PR%
105 ?
106 NEXT
107 IF BS%=0 GOTO 119
108 INPUT "# TO BUY A SHIP (INVALID NUMBER QUITS)"; N
109 IF N > 20 OR N < 0 OR QS$(N) = "" GOTO 119
110 QS$=QS$(N)
111 PR%=QC%(N)
112 ? "DONE!  PRICE=" PR% * 1000000
113 CR = CR - (PR% * 1000000)
114 ? "BALANCE: " CR
115 SH(0,4) = VAL(MID$(QS$,5,1))
116 SH(0,6) = VAL(MID$(QS$,6,1))
117 SH(0,9) = VAL(MID$(QS$,8,3))
118 SH% = N
119 RETURN
120 REM SHOW JUMP MAP
121 ?:? "JUMP RANGE MAP:":? UL$
122 JR=SH(0,6):GOSUB 570
123 FOR N=0 TO 24
124 IF MA$(N) <> "" THEN ? MA$(N)
125 NEXT
126 ?
127 RETURN
128 REM CALC FLUX
129 FL = INT((RND(1)-RND(1))*6)
130 RETURN
131 REM TRADE
132 GOSUB 599
133 ?:INPUT "SELL CARGO TYPE (0-10 OR Q)"; S$
134 IF S$ = "Q" GOTO 141
135 S = VAL(S$) : IF S > 10 GOTO 141
136 ? "SELLING " MID$(CT$(S),4,12)
137 INPUT "HOW MANY TONS (0 TO QUIT)    "; T$
138 T = VAL(T$) : IF T = 0 OR T > CA(S) GOTO 141
139 CA(S) = CA(S) - T
140 CR = CR + CP(S) * T
141 ?:INPUT "BUY CARGO TYPE (0-10 OR Q)"; B$
142 IF B$ = "Q" THEN RETURN
143 B = VAL(B$) : IF B > 10 THEN RETURN
144 ? "BUYING " MID$(CT$(B),4,12)
145 INPUT "HOW MANY TONS (0 TO QUIT)    "; T$
146 T = VAL(T$) : IF T = 0 THEN RETURN
147 CA(B) = CA(B) + T
148 CR = CR - CP(B) * T
149 RETURN
150 ? "INITIALIZING:"
151 REM
152 REM  INITIALIZE TRANSLATION ALPHABETS
153 REM
154 AL$(0) = " ABCDEFGHIJKLMNOPQRSTUVWXYZ?:'-"
155 AL$(1) = " 0123456789ABCDEFGHILMNORSTUWY-"
156 ? " - JUMP RANGE MAP"
157 DIM MA$(24)
158 C = 19
159 R = 10
160 SH(0,4) = 1  :REM MANEUVER
161 SH(0,6) = 2  :REM JUMP 
162 SH(0,9) = 60 :REM CARGO
163 SH% = 1      :REM MARAVA
164 REM READ SHIP QSPS
165 DIM QS$(20) : REM QSPS
166 DIM QC%(20) : REM MCR
167 N = 0
168 ? " - SHIP CATALOG"
169 READ Q$: IF Q$ = "0" GOTO 183
170 QS$(N) = Q$ 
171 REM CALCULATE COST
172 Q7% = VAL(MID$(Q$,7,1))
173 HU% = ASC(MID$(Q$,3,1))-64
174 IF HU% > 14 THEN HU% = HU% - 1 :REM O
175 IF HU% > 8  THEN HU% = HU% - 1 :REM I
176 PR% = 8 * HU%
177 PR% = PR% + HU% * VAL(MID$(Q$,5,1)) :REM M
178 PR% = PR% + HU% * VAL(MID$(Q$,6,1)) :REM J
179 PR% = PR% * Q7%
180 QC%(N) = PR%
181 N=N+1
182 GOTO 169
183 REM
184 REM READ CARGO TYPES
185 REM
186 REM A ROCK-SCISSORS-PAPER CIRCULAR
187 REM RELATIONSHIP.
188 REM
189 REM   1-2: PRODUCER WORLD
190 REM  4-15: CARGO NAME
191 REM    16: BASE PRICE (KCR)
192 REM    17: PRICE MARGIN (MULTIPLE)
193 REM 18-19: CONSUMER WORLD
194 REM
195 DIM CT$(10)
196 ? " - CARGO TYPES"
197 CT$(0)  = "AG TISANES     87RI"
198 CT$(1)  = "AS MINERALS    36NI"
199 CT$(2)  = "BA GEMS        75VA"
200 CT$(3)  = "DE SEEDSTOCK   68AG"
201 CT$(4)  = "FL EXOTICS     22LO"
202 CT$(5)  = "HI CHELATES    52AS"
203 CT$(6)  = "LO FLORA       24BA"
204 CT$(7)  = "NI RARE EARTHS 93HI"
205 CT$(8)  = "PO ARTWORK     49DE"
206 CT$(9)  = "RI BRANDS      11PO"
207 CT$(10) = "VA CRYSTALS    32FL"
208 REM
209 REM READ SHIP CARGO
210 REM
211 REM FORMAT: TONS, TL, TC INDEX
212 REM
213 DIM CA(10)
214 DIM CP(10)
215 ? " - SHIP CARGO"
216 CA(0)  = 5
217 CA(1)  = 5
218 CA(2)  = 5
219 CA(3)  = 5
220 CA(4)  = 5
221 CA(5)  = 5
222 CA(6)  = 5
223 CA(7)  = 5
224 CA(8)  = 5
225 CA(9)  = 5
226 CA(10) = 5
227 REM
228 REM READ TRADE CODE INDEX
229 REM THE MOST COMMON TC MIXES.
230 REM
231 ? " - TRADE CODES"
232 DIM TC$(50)
233 X=0
234 READ TC$ : IF TC$ = "0" GOTO 238
235 TC$(X)=TC$
236 X=X+1
237 GOTO 234
238 REM =============================
239 REM 
240 REM  READ SYSTEM DATA
241 REM 
242 REM =============================
243 DIM SY$(32,40)
244 ? " - SECTOR DATA"
245 READ SY$:IF SY$ = "0" GOTO 251
246 LO$ = LEFT$(SY$,4)
247 C0 = VAL(LEFT$(LO$,2))
248 R0 = VAL(RIGHT$(LO$,2))
249 SY$(C0,R0) = MID$(SY$,5)
250 GOTO 245
251 REM DONE
252 ?:? "DATA LOAD COMPLETE." 
253 RETURN
254 REM -------------------------------------------
255 REM QSP + STRENGTH + CARGO + ALIGNMENT + NAME
256 REM -------------------------------------------
257 DATA S-AL222  3IMURPHY
258 DATA S-AA223  5ISERPENT
259 DATA A-BS111 80 BEOWULF
260 DATA A-BS121 60 MARAVA
261 DATA P-DA643 40*AZ ALRRAK
262 DATA R-DA111200 MARCH
263 DATA G-DL713 10AKHOSAA
264 DATA E-DU523  2IGAZELLE
265 DATA M-FU131120 BRILLIANCE
266 DATA E-FS413 24ZSESAVETL
267 DATA G-FU423  3ZSHIVVA
268 DATA C-HU333 85 BROADSWORD
269 DATA F-KS131400 SUSA
270 DATA R-TB432250 LEVIATHAN
271 DATA 0
272 REM
273 REM  TRADE CODE INDEX TABLE
274 REM
275 DATA ""
276 DATA AG
277 DATA AG NI
278 DATA AG NI RI
279 DATA AG RI
280 DATA AS BA VA
281 DATA AS HI IN VA
282 DATA AS LO VA
283 DATA AS NI VA
284 DATA AS VA
285 DATA BA
286 DATA BA DE PO
287 DATA BA FL
288 DATA BA PO
289 DATA BA VA
290 DATA DE HI IN PO
291 DATA DE HI PO
292 DATA DE LO PO
293 DATA DE NI
294 DATA DE NI PO
295 DATA DE NI RI
296 DATA DE PO
297 DATA DE RI
298 DATA FL
299 DATA FL HI IN
300 DATA FL LO
301 DATA FL NI
302 DATA HI
303 DATA HI IN
304 DATA HI IN PO
305 DATA HI IN VA
306 DATA HI PO
307 DATA LO
308 DATA LO PO
309 DATA LO VA
310 DATA NI
311 DATA NI PO
312 DATA NI RI
313 DATA NI VA
314 DATA PO
315 DATA RI
316 DATA VA
317 DATA 0
318 REM --------------------------------------------
319 REM
320 REM DATA IS IN THIS FORMAT:
321 REM
322 REM    1-4: HEX LOC
323 REM      5: STARPORT + BASES
324 REM    6-7: TRADE CODES INDEX
325 REM      8: TL
326 REM      9: TC1 AN,CP,RE,RS,SA
327 REM     10: BELT, GG, AND ZONE INDICATORS
328 REM     11: ALLEGIANCE
329 REM    12+: NAME
330 REM
331 REM ---------------------------------------------
332 DATA "0101>199;>ZZEYCUDE", "0102>21A;AZRENO", "0103U37B;<ZERRERE"
333 DATA "0104>329;<ZCANTREL", "0108>238;>NGYOMAR", "0202>2 5;=ZTHENGO"
334 DATA "0301>3 8;=NRIO", "0303U 0C;AZGESENTOWN", "0304T27D=<ZCHRONOR"
335 DATA "0307U39A<@ZATSA", "0503?2 8;<NWHENGE", "0601@1 6;>NENLAS-DU"
336 DATA "0605>3 9;<NALGEBASTER", "0607@357;<NRASATT", "0608T 0C;<ZNINJAR"
337 DATA "0610U1 A;>ZSHEYOU", "0703@356;<NINDO", "0704@357;<NNEREWHON"
338 DATA "0705T40C;>ZCIPANGO", "0710@4 2;=NSTAVE", "0805G357;ACNARVAL"
339 DATA "0807@325;<NPLAVEN", "0808M39B;ACQUAR", "0810@259;=CFROND"
340 DATA "0901@268;>NCONDYOLE", "0902>259;=NPUPARKIN", "0904U4 A;>ZCHWISTYOCH"
341 DATA "0909>35A;@CGOUGESTE", "1004>3 8;BNESALIN", "1005E38B;=IRUBY"
342 DATA "1006E2 B;>IEMERALD", "1010?197;>NZENOPIT", "1102>279;BZRIVERLAND"
343 DATA "1103U 0A;AZCLAN", "1106T28C;>IJEWELL", "1110F358;>NZIRCON"
344 DATA "1201@357;>ZAO-DAI", "1204T3 A;=IMONGO", "1209F357;<NUTOLAND"
345 DATA "1210@3 4;@NPEQUAN", "1305?325;AINAKEGE", "1307E35A;>ILYSEN"
346 DATA "1401=358;@ZFOELEN", "1402T38B;BZFARREACH", "1510@130;=N871-438"
347 DATA "1604?298;<ILOUZY", "1607;323?FIGRANT", "1705<28D<;IEFATE"
348 DATA "1706=40A;@IALELL", "1802=239;>IYRES", "1803>317;<IMENORB"
349 DATA "1805=35D;<IUAKYE", "1806@327;>IWHANGA", "1807@4 2@>IKNORBES"
350 DATA "1808?355;>IFORBOLDN", "1809>287;ANRUIE", "1810F359;>IJENGHE"
351 DATA "1903L34D<=IPIXIE", "1904D26D<=IBOUGHENE", "1909F387;<IHEFRY"
352 DATA "1910T40CN=IREGINA", "2005E40B;<IFERI", "2007F287;BIROUP"
353 DATA "2106;352;EIPSCIAS", "2110>22AD>IYORI", "2201F35A;<IDENTUS"
354 DATA "2202<378;>IKINORB", "2204?354<AIBECK'S WORLD", "2205> 07;;IENOPE"
355 DATA "2207@239;AIWOCHIERS", "2303>269;<IYORBUND", "2306>9 8<FISHIONTHY"
356 DATA "2308;274;FIALGINE", "2309@268;>IYURST", "2402=4 5;>IHEYA"
357 DATA "2405@ 05;BIKENG", "2406>35B;=IMOUGHAS", "2408@168;>IRETHE"
358 DATA "2410U1 9<>IINTHE", "2509L329;=IPAYA", "2510>268;AIDHIAN"
359 DATA "2602>2 8<BICORFU", "2607@2 7;>IFOCALINE", "2701=2 A;=ILABLON"
360 DATA "2706@329;<IHEGUZ", "2708>35A;<IVIOLANTE", "2905@100;>IPAVANNE"
361 DATA "2906>35B;=ICARSTEN", "2908@1 7;AIZILA", "3001>327;>IJESEDIPERE"
362 DATA "3002>238;>IYEBAB", "3003E35B;>INASEMIN", "3004?2 6;@IZYKOCA"
363 DATA "3005=277;@IARAMANX", "3008>2 5;AIPYSADI", "3010M35B;=IL'OEUL D'DIEU"
364 DATA "3102=26A;BIRUGBIRD", "3103E35A;BITOWERS", "3104>33C;<IFENETEMAN"
365 DATA "3107?357;EILEWIS", "3110T35B=<IARAMIS", "3202=27B;@IJUNIDY"
366 DATA "3207>8 9<>IPATINIR", "3209M328;=INATOKO", "3210>268;BIREACHER"
367 DATA "0111=358;>NATSON", "0114>26A;>NYIKTOR", "0115@325;<NXHOSA"
368 DATA "0212>356;<NPRINX", "0215@3 4;=CRUSHU", "0218@348;>NBAEL"
369 DATA "0311=358;>NMIZAN-FEL", "0412U34A;=ZSANSIBAR", "0414>388;<NATTICA"
370 DATA "0416@249C@CRETINAE", "0511>329;>ZTERRA NOVA", "0512@355;=ZASMODEUS"
371 DATA "0518?355;<CFAISAL", "0613=35C;=ZLEBEAU", "0614U1 9==ZQUERION"
372 DATA "0618@406;<CDEKALB", "0620@2 6;=SWINSTON", "0712F328;=ZRAPP'S WORLD"
373 DATA "0717=36C;<CTHANBER", "0720@27B;@SENTROPE", "0820>358;=SANSELHOME"
374 DATA "0911>1 5;<NCALORAN", "0912@ 08;<N899-076", "0915=389;=CQUARE"
375 DATA "0919;100?DNZETA 2", "1011=279;@NARDEN", "1018>349;=ICHOLEOSTI"
376 DATA "1020>2 6;@IMARGESI", "1116L38A=<IFRENZIE", "1118E 0A;>IGARDA-VILIS"
377 DATA "1119<28A;<IVILIS", "1212@356;<NDIGITIS", "1213@347;ACEDININA"
378 DATA "1214@100;<I728-907", "1216?268;<ISTELLATIO", "1217@ 06;=IARKADIA"
379 DATA "1311= 0C;=NTREMOUS DEX", "1315P328;<IMIRRIAM", "1320?2 7K<ISAURUS"
380 DATA "1411@357;=NRANGENT", "1413M35A;>IDENOTAM", "1417@325;<IFICANT"
381 DATA "1511>328;@CTIONALE", "1515> 07;=ICALIT", "1519;395;DIASGARD"
382 DATA "1520@100;>ITAVONNI", "1611>3 8;<IPHLUME", "1711M 0A;<IEXTOLAY"
383 DATA "1719T35B=<ILANTH", "1811?38AK=IDINOM", "1815M 0A;=IGHANDI"
384 DATA "1817? 02<FIVICTORIA", "1912E2 9;=IDINOMN", "1916;2 4;FIYLAVEN"
385 DATA "1918?357;BISONTHERT", "1920M19D;>ID'GANZIO", "2011@269;BIWYPOC"
386 DATA "2111;100;FIDJINNI", "2112?1 6;AIRECH", "2212>258;=IK'KIRKA"
387 DATA "2215=36A;BIQUOPIST", "2311?398;<ITREECE", "2313>32A;<IECHISTE"
388 DATA "2314?325;=IPIREMA", "2317>38A;@IRHISE", "2319U35A;>IIVENDO"
389 DATA "2411F327;>IKEANOU", "2414>2 9;>ITUREDED", "2415@353C=IVREIBEFGER"
390 DATA "2416>324;=ILA'BELLE", "2417E 0B;=IEQUUS", "2418M357;=IICETINA"
391 DATA "2419>379;>ICOGRI", "2420N399;=ISKULL", "2512>359;=IKINORB"
392 DATA "2514>326;=IGILEDEN", "2519@269;>IPANNET", "2520E367;<IGARRINCSKI"
393 DATA "2612M8 E;=IMACENE", "2613<32D<<IFULACIN", "2620>279<AINATOKO"
394 DATA "2712L35A;=IRISEK", "2715<27B;=IPOROZLO", "2716T27F=<IRHYLANOR"
395 DATA "2720>327;=ILONESEDA", "2811@355;>IVALHALLA", "2812>24B;BIZIVIJE"
396 DATA "2814L188;>IJAE TELLONA", "2818;100;EIGEROME", "2912<2 B;>IHENOZ"
397 DATA "2913U359;=ICELEPINA", "2918=8 9;<IGITOSY", "3015=2 5;>IBELIZO"
398 DATA "3016@353;BIKEGENA", "3017@358;<IHERONI", "3019; 05;FI457-973"
399 DATA "3020> 0B;=ISOMEM", "3111=359;<IVINORIAN", "3112M328;>INUTEMA"
400 DATA "3114;100;DIHUDERU", "3118=1 6;>ICIPATWE", "3119>405C<IVANEJEN"
401 DATA "3212T32C;<IMARGESI", "3216G15A;BIBEVEY", "3218>19B;=ITACAXEB"
402 DATA "3220>2 5;>IPOWAZA", "0122?194;<NJUNCTION", "0129@357;<NUNIQUA"
403 DATA "0130<41A;@NGAROO", "0223=36B;=DSTERN-STERN", "0230@130;=N886-945"
404 DATA "0321F39A;>NNONYM", "0325E357;>DLABERV", "0326>369;>DEKTRON"
405 DATA "0421>28A;>DZAMINE", "0425>1 8;=DENGRANGE", "0426U 09;=DILIUM"
406 DATA "0427=4 9;<DROGET", "0429@336<<NKARDIN", "0430>2 5;<CBULARIA"
407 DATA "0526?3 3;=DRORRE", "0527T27C;<DMIRE", "0528@1 5;ANCONDARIA"
408 DATA "0530@368;<NDORANNIA", "0622?199;>DTERANT 340", "0624<39D;<DJACENT"
409 DATA "0625;100?DD494-908", "0627<27G<>DDARRIAN", "0721;336;DDTORMENT"
410 DATA "0723>2 9;<DTRIFUGE", "0724=36C;@DNOSEA", "0727F17A;>DSPUME"
411 DATA "0729?327<>NATOR", "0822@2 3;=DCUNNONIC", "0830=329;>CDEBARRE"
412 DATA "0921U409;>SHRUNTING", "0922U40A;>STIZON", "0927U28A;>SNARSIL"
413 DATA "0930<36B;<IFLAMMARION", "1022U3 B;>SCOLADA", "1026U40B;>SANDURIL"
414 DATA "1121U19A;>SMJOLNIR", "1123U4 A;ASJOYEUSE", "1126U23A;=SORCRIST"
415 DATA "1130H194;@SENOS", "1221E1 8;>SGUNGNIR", "1223T28C;=SGRAM"
416 DATA "1225U 0A;=SEXCALIBUR", "1324U 0A;=STYRFING", "1325T28C;=SSACNOTH"
417 DATA "1329E4 A;<ICALADBOLG", "1424U3 A;<SBEATER", "1429@328;AIGUNN"
418 DATA "1430@8 A;>ICALIBURN", "1522U 0A;=SDYRNWYN", "1523U4 B;>SDURENDAL"
419 DATA "1524U40A;=SHOFUD", "1525U1 A;=SSTING", "1526U2 A;=SBITER"
420 DATA "1529@100?>SSTEEL", "1626@100?>SIRON", "1627@ 00?<SBRONZE"
421 DATA "1628@100?=SMITHRIL", "1721>34C;<IARBA", "1727E35B;=IWARDN"
422 DATA "1728>327;<IOLYMPIA", "1729>219;=ISMOUG", "1822G 06;BIRABWHAR"
423 DATA "1824L 0B;AIADABICCI", "1825=8 B;=IZAIBON", "1826<1 E<<ITENALPHI"
424 DATA "1924@205;>IIANIC", "1927F408;>ISPIRELLE", "2024F 08;=IDERCHON"
425 DATA "2124T28D=<ILUNION", "2125E2 B;BISHIRENE", "2128?325;@IPENKWHAR"
426 DATA "2129>219;<IHARVOSETTE", "2224>329;=ICARSE", "2228= 0A;>IPERSEPHONE"
427 DATA "2321=328;>IQUIRU", "2322;322;EIGORRAM", "2323E32B;=IRESTEN"
428 DATA "2324M1 A;<ICAPON", "2325>32A;=ISHARRIP", "2327L28D;<ISTROUDEN"
429 DATA "2425@7 8;=IGANDR", "2426@258;=IDROLRAW", "2521=288;BIHERONI"
430 DATA "2523=3 6;>IBYRET", "2527@347;=IPIMANE", "2621<36A;<IFOSEY"
431 DATA "2624U2 8;=IMERCURY", "2627>358;=ITIVID", "2726>329;<ICAREY"
432 DATA "2728<39BCAIDUALE", "2824>199;@ICATUZ", "2827F325;>IMELETO"
433 DATA "2828M358;>IHEXOS", "2830F 07;=IPEDASE", "2924N328;=IMORAN"
434 DATA "2927< 0B;>IMAITZ", "2930F33A;=IMAINZ", "3021>357;>IBRODIE"
435 DATA "3022>32A;=IRORISE", "3024= 07;@IJOKOTRE", "3025<27C;=IFORNICE"
436 DATA "3026@327;=IGRILLE", "3029< 0E;@IPALIQUE", "3030F358;=INEXINE"
437 DATA "3123G177;<INADRIN", "3124T28F;>IMORA", "3223F 07;<IDOJODO"
438 DATA "3228>329;BIFENL'S GREN", "0133M2 B;AIEMAPE", "0139M17B;@IRAWEH"
439 DATA "0140@323;=N876-574", "0231@268;<NSAXE", "0236>1 9<EIANDOR"
440 DATA "0240@358;>N769-422", "0332@367;@IGOTHE", "0333= 0A;BIMIRRIAM"
441 DATA "0336>358<DICANDORY", "0340@354;<NWONDERAY", "0433M 09;@IJONE"
442 DATA "0440?347;ANJINX", "0532?2 7;<CUCELLA", "0533>204;>IPENELOPE"
443 DATA "0534T4 C;@IKARIN", "0538M1 7;@IWONSTAR", "0539>329C=IFROIN"
444 DATA "0632F1 7;>IGOHATURE", "0637> 09;@IQUHAIATHAT", "0638@357;AILAKOU"
445 DATA "0731@358;=NRALHE", "0732L4 C==IIDERATI", "0739@ 07;=NTONDOUL"
446 DATA "0834H357;@C875-496", "0837@2 7;<NOCHECATE", "0838?4 5;=NMEWEY"
447 DATA "0840@349;>N975-452", "0931=26A;<NASTELTINE", "0938?17A;>NINCHIN"
448 DATA "0940?396;=NSINGER", "1031@130;<N567-908", "1037>36A;>NAVASTAN"
449 DATA "1040> 0A;<NKUAI QING", "1131@355;<NFALDOR", "1132G7 9;>CBOWMAN"
450 DATA "1133>359;<NSQUALLIA", "1138=3 A;=CTARSUS", "1232F328;=CWALSTON"
451 DATA "1233@268;<NFLEXOS", "1237E28D;=CCOLLACE", "1238>406;ANPAVABID"
452 DATA "1331@358;=NDATRILLIAN", "1332;140;FNNIRTON", "1337@120C>CJUDICE"
453 DATA "1339=40C;>NTREXALON", "1340M3 5;<CMOTMOS", "1433@238;=NNOCTOCOL"
454 DATA "1434F3 7;@CTARKINE", "1435=409;<CDALLIA", "1436@369;<NTALOS"
455 DATA "1531@100;>NDAWNWORLD", "1532M358;=CELIXABETH", "1533? 0A;<NFORINE"
456 DATA "1537E40B;<IMERTACTOR", "1631>268;ACTALCHEK", "1632@ 07;<NMILAGRO"
457 DATA "1634>404;>NPAGATON", "1635<34A;<NBINGES", "1637U26C;=IMILLE FALCS"
458 DATA "1731<38B;>IGROTE", "1733@357;=ILYDIA", "1736?197;BIMELIOR"
459 DATA "1737M269;>IEGYPT", "1739>359;=IASTER", "1836@216;<ICALLIA"
460 DATA "1932>268;=IMITHRAS", "1934<35B;AIWEISS", "1935>359;<IWINDSOR"
461 DATA "1937=2 9;>IOVERNALE", "1938M 0B;=INEW ROME", "1939>355;>ICRAW"
462 DATA "2035=299;BIAKI", "2036T6 F;>IGLISTEN", "2038>35B<=ITRANE"
463 DATA "2132@367;<ICENTRY", "2134>365;<ICALEDONIA", "2137@3 2;>ISOREL"
464 DATA "2138F 0A;<IHOROSHO", "2140U198;>IROMAR", "2231G4 5?BIMARASTAN"
465 DATA "2232@277;BICROUT", "2233>24B;>ITIREM", "2234>38B;>IINTHE"
466 DATA "2236?197;=ITSARINA", "2237F32A;@IWURZBURG", "2331@352;<IBICORNN"
467 DATA "2334< 0C;AIFFUDN", "2336T2 C;<IBENDOR", "2534>378;=IBURTSON"
468 DATA "2536<38B;=ISQUANINE", "2537D19A;>IDOBHAM", "2538@322;>IPYRAMUS"
469 DATA "2539@196;BITHISBE", "2540= 06;>IARAMIS", "2637>8 C;AIROBIN"
470 DATA "2638@ 05;<ID'MARA", "2639>359;=IKELTCHNER", "2731=327;<ITUSSINIAN"
471 DATA "2733<40B;BIEDENELT", "2735G2 7;>ICONWAY", "2739F297;BIDODDS"
472 DATA "2832@325;=ILEANDER", "2833?2 3;=IPEPERNIUM", "2834=186<<ITRALTHA"
473 DATA "2839F357;=IFARQUAHAR", "2933@2 6;=IRAYDRAD", "2934=357;=IZYRA"
474 DATA "2935M356;=IMURCHISON", "2936<36B;BIHAMMERMIUM"
475 DATA "2940G358;=ITHORNNASTOR"
476 DATA "3032=36B;=IKATARULU", "3035=2 6;<IPRILISSA", "3038>359;=ITEE-TEE-TEE"
477 DATA "3039<32B;=IYOUGHAL", "3040G359;=ITENELPHI", "3138>2 5;AIZEPHYR"
478 DATA "3139E2 5;>ICHAMOIS", "3233= 08;>IRAMIVA", "3235T28F==ITRIN"
479 DATA "3236>1 5;@IHAZEL"
480 DATA 0
481 REM  CARGO PRICE PER TON
482 REM  (CA,UW$,TC$,FLUX) => PR
483 TL$ = RIGHT$(UW$,1)
484 TL = ASC(TL$)-48
485 IF TL > 16 THEN TL = TL - 7
486 PR = 100 + TL * 10
487 CT$ = CT$(CA)
488 PR = PR * VAL(MID$(CT$,16,1)) + FL * 10
489 IF LEFT$(CT$,2) = LEFT$(TC$,2) THEN PR = PR - 100
490 IF RIGHT$(CT$,2) = LEFT$(TC$,2) THEN PR = PR + 100 * VAL(MID$(CT$,17,1))
491 IF PR < 1 THEN PR = 10 :REM STOPGAP
492 RETURN
493 REM COMPOSE_HEX(C,R) -> C$,R$,LO$
494 C$ = "0" + MID$(STR$(C),2)
495 R$ = MID$(STR$(R),2)
496 IF R < 10 THEN R$ = "0" + R$
497 LO$ = C$ + R$
498 RETURN
499 REM COMPUTE-LOC(C,R) -> C$,R$,LO$
500 C$ = MID$(STR$(C),2)
501 IF C < 10 THEN C$ = "0" + C$
502 R$ = MID$(STR$(R),2)
503 IF R < 10 THEN R$ = "0" + R$
504 LO$ = C$ + R$
505 RETURN
506 REM IN$ HAS "ORDERED" UPP
507 REM UP$ HAS CANONICAL UPP
508 FOR X = 1 TO 6
509 VA = 10 - X
510 IF MID$(IN$,X,1) = "S" THEN UU(1) = VA
511 IF MID$(IN$,X,1) = "D" THEN UU(2) = VA
512 IF MID$(IN$,X,1) = "E" THEN UU(3) = VA
513 IF MID$(IN$,X,1) = "I" THEN UU(4) = VA
514 IF MID$(IN$,X,1) = "U" THEN UU(5) = VA
515 IF MID$(IN$,X,1) = "C" THEN UU(6) = VA
516 NEXT
517 UP$ = ""
518 FOR X = 1 TO 6
519 UP$ = UP$ + MID$(STR$(UU(X)),2)
520 NEXT
521 RETURN
522 REM
523 REM  DECODE SY$ INTO ITS COMPONENT PARTS:
524 REM
525 REM  D6323312TTGNEAEGCOR
526 REM      1: STARPORT + BASES
527 REM    2-3: TC INDEX
528 REM      4: TL
529 REM      5: TC1 AN,CP,RE,RS,SA
530 REM      6: BELT, GG, AND ZONE INDICATORS
531 REM      7: ALLEGIANCE
532 REM     8+: NAME
533 REM
534 SP=ASC(LEFT$(SY$,1))-59
535 BA$=" "
536 IF SP AND 8  =  8 THEN BA$ = "S"
537 IF SP AND 16 = 16 THEN BA$ = "N"
538 IF SP AND 24 = 24 THEN BA$ = "2"
539 SP = SP AND 7
540 SP$ = CHR$(64+SP)
541 IF SP = 0 THEN SP$ = "X"
542 UW$ = SP$ + "-" + MID$(SY$,4,1)
543 ZO = ASC(MID$(SY$,6,1))-59
544 BE = 0: GG = 0: ZO$ = " "
545 IF ZO AND 1 = 1 THEN BE = 1
546 IF ZO AND 2 = 2 THEN GG = 1
547 IF ZO AND 4 = 4 THEN ZO$ = "A"
548 IF ZO AND 8 = 8 THEN ZO$ = "R"
549 AL$ = MID$(SY$,7,1)
550 TX = VAL(MID$(SY$,2,2))
551 TC$ = TC$(TX)
552 T1 = ASC(MID$(SY$,5,1))-59
553 IF (T1 AND 1)  = 1  THEN TC$ = TC$ + " AN"
554 IF (T1 AND 2)  = 2  THEN TC$ = TC$ + " CP"
555 IF (T1 AND 4)  = 4  THEN TC$ = TC$ + " RE"
556 IF (T1 AND 8)  = 8  THEN TC$ = TC$ + " RS"
557 IF (T1 AND 16) = 16 THEN TC$ = TC$ + " SA"
558 NA$ = MID$(SY$,8)
559 RETURN
560 REM DISTANCE(C,R,C1,R1) -> D%
561 A1 = R + INT(C/2)
562 A2 = R1 + INT(C1/2)
563 D1% = ABS(A1-A2)
564 D2% = ABS(C-C1)
565 D3% = ABS( (A1-C) - (A2-C1) )
566 D% = D3%
567 IF (D1% >= D2%) AND (D1% >= D3%) THEN D% = D1%
568 IF (D2% >= D1%) AND (D2% >= D3%) THEN D% = D2%
569 RETURN
570 REM JUMP-MAP(C,R,JR) -> MA$()
571 FOR NN=0 TO 24 : MA$(NN) = "" : NEXT
572 NN%=0
573 FOR C1 = C-JR TO C+JR : IF C1 < 0 OR C1 > 32 THEN NEXT
574 FOR R1 = R-JR TO R+JR : IF R1 < 0 OR R1 > 40 THEN NEXT
575 IF NN% = 24 THEN 590
576 REM TOO CLOSE/FAR?
577 GOSUB 560: IF (D% > JR) GOTO 590
578 REM EMPTY HEX?
579 IF SY$(C1,R1) = "" GOTO 590
580 SY$ = SY$(C1,R1):GOSUB 522:REM PARSE SY$
581 X$ = MID$(STR$(C1),2)
582 Y$ = MID$(STR$(R1),2)
583 IF C1 < 10 THEN X$ = "0" + X$
584 IF R1 < 10 THEN Y$ = "0" + Y$
585 NN% = NN% + 1
586 NA$=LEFT$(NA$+"                    ",12)
587 TC$=LEFT$(TC$+"                    ",12)
588 D$=MID$(STR$(D%),2)
589 MA$(NN%) = X$+Y$+" "+D$+" "+NA$+" "+TC$
590 NEXT:NEXT
591 SY$ = SY$(C,R): REM CLEAN UP!
592 RETURN
593 REM C1,R1,D%,JR -> C,R,SY$
594 REM IF D%>JR, NO GO
595 IF (D% > JR) THEN RETURN
596 C = C1: R = R1
597 SY$ = SY$(C,R)
598 RETURN
599 REM SHOW CARGO PRICES
600 ?:? "CARGO PRICES PER TON:"
601 FOR CA = 0 TO 10
602 GOSUB 522 :REM UW$, TC$
603 GOSUB 481
604 PR$ = LEFT$(MID$(STR$(PR)+"                    ",2),5)
605 ? CA, MID$(CT$(CA),4,12) "CR " PR$ " (" CA(CA) "T)"
606 CP(CA) = PR
607 NEXT
608 RETURN
