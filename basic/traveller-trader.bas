1 ? " *************************************"
2 ? " *                                   *"
3 ? " *     TRAVELLER TRADER 2020.03      *"
4 ? " *                                   *"
5 ? " *  - PILOT A BEOWULF-2 FREE TRADER  *"
6 ? " *  - BUY AND SELL CARGO             *"
7 ? " *  - EXPLORE THE SPINWARD MARCHES   *"
8 ? " *                                   *"
9 ? " *************************************"
10 ?
11 REM INITIALIZE GAME
12 GOSUB 140
13 F = FRE(1)
14 GOSUB 119
15 UL$="------------------------------"
16 MV%=0
17 ?:? "FREE MEMORY: " F "BYTES"
18 ?
19 REM MAIN LOOP
20 GOSUB 84
21 ? "MENU" : ? UL$
22 ? " (B)YE"
23 ? " (C)REW"
24 ? " (M)AP"
25 ? " (S)HIP STATUS"
26 ? " (T)RADE"
27 ? " (####) JUMP TO HEX"
28 IN$ = "" : INPUT IN$
29 IF IN$ = "B" THEN GOTO 38
30 IF IN$ = "C" THEN GOSUB 90
31 IF IN$ = "M" THEN GOSUB 100
32 IF IN$ = "S" THEN GOSUB 108
33 IF IN$ = "T" THEN GOSUB 122
34 IF IN$ = "" GOTO 20
35 IF VAL(IN$) > 0 THEN GOSUB 66
36 GOTO 20
37 REM BYE AND SHOW SCORE
38 ? " *************************************"
39 ? " *                                   *"
40 ? " *      THANK YOU FOR PLAYING        *"
41 ? " *        TRAVELLER TRADER!          *"
42 ? " *                                   *"
43 ? " *************************************"
44 ?:? "YOUR STATISTICS:" :? UL$
45 ? " - JUMPS:    " MV%
46 ? " - CREDITS:  " CR "
47 IF MV%>0 THEN ? " - CR/JUMP:  " INT(CR/MV%)
48 ?
49 ? "PROGRAM WRITTEN BY ROBERT EAGLESTONE."
50 ?
51 ? "THE TRAVELLER GAME IN ALL FORMS IS "
52 ? " - OWNED BY FAR FUTURE ENTERPRISES. "
53 ? " - COPYRIGHT 1977 - 2020 FAR FUTURE "
54 ? "   ENTERPRISES. "
55 ? " - A REGISTERED TRADEMARK OF FAR"
56 ? "   FUTURE ENTERPRISES. "
57 ? " THIS PROGRAM IS FOR PERSONAL,"
58 ? "   NON-COMMERCIAL USE ONLY. "
59 ? " ANY USE OF FAR FUTURE ENTERPRISES"
60 ? "   COPYRIGHTED MATERIAL OR TRADEMARKS "
61 ? "   ANYWHERE IN THIS PROGRAM SHOULD NOT"
62 ? "   BE VIEWED AS A CHALLENGE TO THOSE"
63 ? "   COPYRIGHTS OR TRADEMARKS."
64 END
65 REM DO A JUMP
66 ?:? "COMPUTING JUMP TO HEX " IN$
67 C1 = VAL(LEFT$(IN$,2))       :REM DESTINATION COL
68 R1 = VAL(RIGHT$(IN$,2))      :REM DESTINATION ROW
69 JR  = SH(0,6)                :REM SHIP JUMP RATING
70 GOSUB 626    :REM POPULATES D% = DISTANCE
71 GOSUB 659 :REM OK GO
72 GOSUB 565
73 SY$=SY$(C,R)
74 GOSUB 588
75 FOR TT=0 TO 2000:NEXT
76 ?:? " - ENGAGING JUMP DRIVE."
77 FOR TT=0 TO 1000:NEXT
78 ?:? " - IN JUMPSPACE. PLEASE STAND BY..."
79 F = FRE(1)
80 IF IN$ = LO$ THEN MV%=MV%+1:?:? "WELCOME TO " NA$ : GOSUB 119
81 IF IN$ <> LO$ THEN ?:? "JUMP FAILURE"
82 RETURN
83 REM CURRENT STATUS
84 SY$ = SY$(C,R)
85 ?:? "BALANCE: CR" CR
86 GOSUB 588
87 ?:? "(" NA$ " " UW$ ") "; 
88 RETURN
89 REM SHOW CREW
90 ?:? "CREW STATUS:":? UL$
91 FOR N=0 TO 9
92 IF PN$(N) = "" THEN 96
93 IN$=PP$(N)
94 GOSUB 572 :REM -> UP$
95 ? N ". " PN$(N) ", " PT$(N) ", " PS$(N) ", " UP$
96 NEXT
97 ?
98 RETURN
99 REM SHOW JUMP MAP
100 ?:? "JUMP RANGE MAP:":? UL$
101 JR=SH(0,6):GOSUB 636
102 FOR N=0 TO 24
103 IF MA$(N) <> "" THEN ? MA$(N)
104 NEXT
105 ?
106 RETURN
107 REM SHOW SHIP
108 ?:? "SHIP DISPOSITION:":? UL$
109 FOR N=0 TO 20
110 IF SH(0,N) = 0 THEN 115
111 IF N < 10 THEN ? " ";
112 ? N ". " LEFT$(CO$(N)+"                    ",15) ": " SH(0,N);
113 IF DD(0,N) THEN ? " DAMAGE: " DD(0,N);
114 ?
115 NEXT
116 ?
117 RETURN
118 REM CALC FLUX
119 FL = INT((RND(1)-RND(1))*6)
120 RETURN
121 REM TRADE
122 GOSUB 665
123 ?:INPUT "SELL CARGO TYPE (0-10 OR Q)"; S$
124 IF S$ = "Q" GOTO 131
125 S = VAL(S$) : IF S > 10 GOTO 131
126 ? "SELLING " MID$(CT$(S),4,12)
127 INPUT "HOW MANY TONS (0 TO QUIT)    "; T$
128 T = VAL(T$) : IF T = 0 OR T > CA(S) GOTO 131
129 CA(S) = CA(S) - T
130 CR = CR + CP(S) * T
131 ?:INPUT "BUY CARGO TYPE (0-10 OR Q)"; B$
132 IF B$ = "Q" THEN RETURN
133 B = VAL(B$) : IF B > 10 THEN RETURN
134 ? "BUYING " MID$(CT$(B),4,12)
135 INPUT "HOW MANY TONS (0 TO QUIT)    "; T$
136 T = VAL(T$) : IF T = 0 THEN RETURN
137 CA(B) = CA(B) + T
138 CR = CR - CP(B) * T
139 RETURN
140 ? "INITIALIZING:"
141 REM
142 REM  INITIALIZE TRANSLATION ALPHABETS
143 REM
144 AL$(0) = " ABCDEFGHIJKLMNOPQRSTUVWXYZ?:'-"
145 AL$(1) = " 0123456789ABCDEFGHILMNORSTUWY-"
146 ? " - JUMP RANGE MAP"
147 DIM MA$(24)
148 REM
149 REM READ SHIP COMPONENT TYPES
150 REM
151 DIM CO$(20)
152 ? " - SHIP COMPONENTS"
153 READ N: IF N = -1 GOTO 156
154 READ CO$(N)
155 GOTO 153
156 REM
157 REM READ PLAYER'S SHIP
158 REM
159 DIM SH(0,20) : REM COMPONENTS
160 DIM DD(0,20) : REM DAMAGE
161 ? " - PLAYER'S SHIP"
162 C = 19
163 R = 10
164 READ N: IF N = -1 GOTO 167
165 READ SH(0,N)
166 GOTO 162
167 REM
168 REM READ CARGO TYPES
169 REM
170 REM A ROCK-SCISSORS-PAPER CIRCULAR
171 REM RELATIONSHIP.
172 REM
173 REM   1-2: PRODUCER WORLD
174 REM  4-15: CARGO NAME
175 REM    16: BASE PRICE (KCR)
176 REM    17: PRICE MARGIN (MULTIPLE)
177 REM 18-19: CONSUMER WORLD
178 REM
179 DIM CT$(10)
180 ? " - CARGO TYPES"
181 CT$(0)  = "AG TISANES     87RI"
182 CT$(1)  = "AS MINERALS    36NI"
183 CT$(2)  = "BA GEMS        75VA"
184 CT$(3)  = "DE SEEDSTOCK   68AG"
185 CT$(4)  = "FL EXOTICS     22LO"
186 CT$(5)  = "HI CHELATES    52AS"
187 CT$(6)  = "LO FLORA       24BA"
188 CT$(7)  = "NI RARE EARTHS 93HI"
189 CT$(8)  = "PO ARTWORK     49DE"
190 CT$(9)  = "RI BRANDS      11PO"
191 CT$(10) = "VA CRYSTALS    32FL"
192 REM
193 REM READ SHIP CARGO
194 REM
195 REM FORMAT: TONS, TL, TC INDEX
196 REM
197 DIM CA(10)
198 DIM CP(10)
199 ? " - SHIP CARGO"
200 CA(0)  = 5
201 CA(1)  = 5
202 CA(2)  = 5
203 CA(3)  = 5
204 CA(4)  = 5
205 CA(5)  = 5
206 CA(6)  = 5
207 CA(7)  = 5
208 CA(8)  = 5
209 CA(9)  = 5
210 CA(10) = 5
211 REM 
212 REM  READ CHARACTER DATA INTO CH$()
213 REM 
214 DIM PN$(9) : REM CHAR NAME
215 DIM PP(9)  : REM CHAR LOCATIONS
216 DIM PP$(9) : REM CHAR PROFILE
217 DIM PT$(9) : REM SOPHONT TYPE
218 DIM PS$(9) : REM CHAR STATUS OK,OOA,DEAD
219 ? " - CREW"
220 READ N:IF N = -1 GOTO 228
221 READ PP,CH$
222 PS$(N)="OK  "
223 PP(N)=PP
224 PN$(N)=LEFT$(CH$,15)
225 PT$(N)=MID$(CH$,16,12)
226 PP$(N)=MID$(CH$,29)
227 GOTO 220
228 REM
229 REM READ TRADE CODE INDEX
230 REM THE MOST COMMON TC MIXES.
231 REM
232 ? " - TRADE CODES"
233 DIM TC$(50)
234 X=0
235 READ TC$ : IF TC$ = "0" GOTO 239
236 TC$(X)=TC$
237 X=X+1
238 GOTO 235
239 REM =============================
240 REM 
241 REM  READ SYSTEM DATA
242 REM 
243 REM =============================
244 DIM SY$(32,40)
245 ? " - SECTOR DATA"
246 READ SY$:IF SY$ = "0" GOTO 252
247 LO$ = LEFT$(SY$,4)
248 C0 = VAL(LEFT$(LO$,2))
249 R0 = VAL(RIGHT$(LO$,2))
250 SY$(C0,R0) = MID$(SY$,5)
251 GOTO 246
252 REM DONE
253 ?:? "DATA LOAD COMPLETE." 
254 RETURN
255 REM
256 REM  SHIP COMPONENT DATA
257 REM
258 DATA 0,HULL
259 DATA 1,BRIDGE
260 DATA 2,SENSORS
261 DATA 3,COMPUTER
262 DATA 4,MANEUVER DRIVE
263 DATA 5,POWER PLANT
264 DATA 6,JUMP DRIVE
265 DATA 7,LIFE SUPPORT
266 DATA 8,CARGO HOLD
267 DATA 9,STATEROOMS
268 DATA 10,LOW BERTHS
269 DATA 11,SANDCASTER
270 DATA 12,MISSILE RACK
271 DATA 13,PULSE LASER
272 DATA 14,BEAM LASER 
273 DATA 15,PARTICLE BEAM
274 DATA 16,BLACK GLOBE
275 DATA -1
276 REM
277 REM  PLAYER'S SHIP DATA
278 REM
279 DATA 0, 200: REM HULL TONS
280 DATA 1, 1  : REM BRIDGE RATING     
281 DATA 2, 1  : REM SENSOR RATING     
282 DATA 3, 2  : REM COMPUTER MODEL    
283 DATA 4, 1  : REM M-DRIVE RATING    
284 DATA 5, 2  : REM P-PLANT RATING    
285 DATA 6, 2  : REM J-DRIVE RATING    
286 DATA 7, 1  : REM LS RATING         
287 DATA 8, 60 : REM HOLD TONS         
288 DATA 9, 10 : REM STATEROOMS        
289 DATA 10, 4 : REM LOW
290 DATA 11, 1 : REM SANDCASTERS
291 DATA 12, 1 : REM MISSILE LAUNCHERS
292 DATA 13, 1 : REM PULSE LASERS
293 DATA -1      
294 REM -------------------------------------------
295 REM CHARACTER DATA IS IN THIS FORMAT:
296 REM
297 REM N = UNIQUE INDEX
298 REM CH$ = CHARACTER NAME
299 REM SK$ = CHARACTER PROFILE
300 REM
301 REM       1: SOPHONT TYPE          SKILL AFFINITY
302 REM          A: ASLAN              GUNNER
303 REM          B: BWAP               ADMIN
304 REM          D: DROYNE             ENGINEER 
305 REM          H: HUMAN              -
306 REM          L: LLELLEWYLOLY       MEDIC
307 REM          M: AMINDII            ASTROGATOR 
308 REM          S: SHRIEKER           SENSOP
309 REM          V: VARGR              PILOT
310 REM
311 REM       2: UPP
312 REM          ORDERED HIGH TO LOW.
313 REM          S - STR - FIGHTER
314 REM          D - DEX - PILOT
315 REM          E - END - GUNNER, SENSOP
316 REM          I - INT - ADMIN, TRADER
317 REM          U - EDU - ASTROGATOR, ENGINEER
318 REM          C - SOC - MEDIC, STEWARD
319 REM
320 REM          FOR EXAMPLE, JAMISON IS DUISEC.
321 REM          THIS MEANS:
322 REM          DEX IS 9
323 REM          EDU IS 8
324 REM          INT IS 7
325 REM          STR IS 6
326 REM          END IS 5
327 REM          SOC IS 4
328 REM
329 REM -------------------------------------------
330 DATA 0, 0, JAMISON        HUMAN        DUISEC
331 DATA 1, 1, AARLANRROUGHL  VARGR        SUDEIC
332 DATA 2, 2, REJNALDI       HUMAN        CIEDSU
333 DATA 3, 3, LISSANDA       HUMAN        CDIEUS 
334 DATA 4, 4, JORN           AMINDII      ESDIUC
335 DATA 5, 5, KURAN          HUMAN        SECICU
336 DATA 6, 6, OCLHHOLLO      LLELLEWYLOLY IDUCES
337 DATA -1
338 REM
339 REM  TRADE CODE INDEX TABLE
340 REM
341 DATA ""
342 DATA AG
343 DATA AG NI
344 DATA AG NI RI
345 DATA AG RI
346 DATA AS BA VA
347 DATA AS HI IN VA
348 DATA AS LO VA
349 DATA AS NI VA
350 DATA AS VA
351 DATA BA
352 DATA BA DE PO
353 DATA BA FL
354 DATA BA PO
355 DATA BA VA
356 DATA DE HI IN PO
357 DATA DE HI PO
358 DATA DE LO PO
359 DATA DE NI
360 DATA DE NI PO
361 DATA DE NI RI
362 DATA DE PO
363 DATA DE RI
364 DATA FL
365 DATA FL HI IN
366 DATA FL LO
367 DATA FL NI
368 DATA HI
369 DATA HI IN
370 DATA HI IN PO
371 DATA HI IN VA
372 DATA HI PO
373 DATA LO
374 DATA LO PO
375 DATA LO VA
376 DATA NI
377 DATA NI PO
378 DATA NI RI
379 DATA NI VA
380 DATA PO
381 DATA RI
382 DATA VA
383 DATA 0
384 REM --------------------------------------------
385 REM
386 REM DATA IS IN THIS FORMAT:
387 REM
388 REM    1-4: HEX LOC
389 REM      5: STARPORT + BASES
390 REM    6-7: TRADE CODES INDEX
391 REM      8: TL
392 REM      9: TC1 AN,CP,RE,RS,SA
393 REM     10: BELT, GG, AND ZONE INDICATORS
394 REM     11: ALLEGIANCE
395 REM    12+: NAME
396 REM
397 REM ---------------------------------------------
398 DATA "0101>199;>ZZEYCUDE", "0102>21A;AZRENO", "0103U37B;<ZERRERE"
399 DATA "0104>329;<ZCANTREL", "0108>238;>NGYOMAR", "0202>2 5;=ZTHENGO"
400 DATA "0301>3 8;=NRIO", "0303U 0C;AZGESENTOWN", "0304T27D=<ZCHRONOR"
401 DATA "0307U39A<@ZATSA", "0503?2 8;<NWHENGE", "0601@1 6;>NENLAS-DU"
402 DATA "0605>3 9;<NALGEBASTER", "0607@357;<NRASATT", "0608T 0C;<ZNINJAR"
403 DATA "0610U1 A;>ZSHEYOU", "0703@356;<NINDO", "0704@357;<NNEREWHON"
404 DATA "0705T40C;>ZCIPANGO", "0710@4 2;=NSTAVE", "0805G357;ACNARVAL"
405 DATA "0807@325;<NPLAVEN", "0808M39B;ACQUAR", "0810@259;=CFROND"
406 DATA "0901@268;>NCONDYOLE", "0902>259;=NPUPARKIN", "0904U4 A;>ZCHWISTYOCH"
407 DATA "0909>35A;@CGOUGESTE", "1004>3 8;BNESALIN", "1005E38B;=IRUBY"
408 DATA "1006E2 B;>IEMERALD", "1010?197;>NZENOPIT", "1102>279;BZRIVERLAND"
409 DATA "1103U 0A;AZCLAN", "1106T28C;>IJEWELL", "1110F358;>NZIRCON"
410 DATA "1201@357;>ZAO-DAI", "1204T3 A;=IMONGO", "1209F357;<NUTOLAND"
411 DATA "1210@3 4;@NPEQUAN", "1305?325;AINAKEGE", "1307E35A;>ILYSEN"
412 DATA "1401=358;@ZFOELEN", "1402T38B;BZFARREACH", "1510@130;=N871-438"
413 DATA "1604?298;<ILOUZY", "1607;323?FIGRANT", "1705<28D<;IEFATE"
414 DATA "1706=40A;@IALELL", "1802=239;>IYRES", "1803>317;<IMENORB"
415 DATA "1805=35D;<IUAKYE", "1806@327;>IWHANGA", "1807@4 2@>IKNORBES"
416 DATA "1808?355;>IFORBOLDN", "1809>287;ANRUIE", "1810F359;>IJENGHE"
417 DATA "1903L34D<=IPIXIE", "1904D26D<=IBOUGHENE", "1909F387;<IHEFRY"
418 DATA "1910T40CN=IREGINA", "2005E40B;<IFERI", "2007F287;BIROUP"
419 DATA "2106;352;EIPSCIAS", "2110>22AD>IYORI", "2201F35A;<IDENTUS"
420 DATA "2202<378;>IKINORB", "2204?354<AIBECK'S WORLD", "2205> 07;;IENOPE"
421 DATA "2207@239;AIWOCHIERS", "2303>269;<IYORBUND", "2306>9 8<FISHIONTHY"
422 DATA "2308;274;FIALGINE", "2309@268;>IYURST", "2402=4 5;>IHEYA"
423 DATA "2405@ 05;BIKENG", "2406>35B;=IMOUGHAS", "2408@168;>IRETHE"
424 DATA "2410U1 9<>IINTHE", "2509L329;=IPAYA", "2510>268;AIDHIAN"
425 DATA "2602>2 8<BICORFU", "2607@2 7;>IFOCALINE", "2701=2 A;=ILABLON"
426 DATA "2706@329;<IHEGUZ", "2708>35A;<IVIOLANTE", "2905@100;>IPAVANNE"
427 DATA "2906>35B;=ICARSTEN", "2908@1 7;AIZILA", "3001>327;>IJESEDIPERE"
428 DATA "3002>238;>IYEBAB", "3003E35B;>INASEMIN", "3004?2 6;@IZYKOCA"
429 DATA "3005=277;@IARAMANX", "3008>2 5;AIPYSADI", "3010M35B;=IL'OEUL D'DIEU"
430 DATA "3102=26A;BIRUGBIRD", "3103E35A;BITOWERS", "3104>33C;<IFENETEMAN"
431 DATA "3107?357;EILEWIS", "3110T35B=<IARAMIS", "3202=27B;@IJUNIDY"
432 DATA "3207>8 9<>IPATINIR", "3209M328;=INATOKO", "3210>268;BIREACHER"
433 DATA "0111=358;>NATSON", "0114>26A;>NYIKTOR", "0115@325;<NXHOSA"
434 DATA "0212>356;<NPRINX", "0215@3 4;=CRUSHU", "0218@348;>NBAEL"
435 DATA "0311=358;>NMIZAN-FEL", "0412U34A;=ZSANSIBAR", "0414>388;<NATTICA"
436 DATA "0416@249C@CRETINAE", "0511>329;>ZTERRA NOVA", "0512@355;=ZASMODEUS"
437 DATA "0518?355;<CFAISAL", "0613=35C;=ZLEBEAU", "0614U1 9==ZQUERION"
438 DATA "0618@406;<CDEKALB", "0620@2 6;=SWINSTON", "0712F328;=ZRAPP'S WORLD"
439 DATA "0717=36C;<CTHANBER", "0720@27B;@SENTROPE", "0820>358;=SANSELHOME"
440 DATA "0911>1 5;<NCALORAN", "0912@ 08;<N899-076", "0915=389;=CQUARE"
441 DATA "0919;100?DNZETA 2", "1011=279;@NARDEN", "1018>349;=ICHOLEOSTI"
442 DATA "1020>2 6;@IMARGESI", "1116L38A=<IFRENZIE", "1118E 0A;>IGARDA-VILIS"
443 DATA "1119<28A;<IVILIS", "1212@356;<NDIGITIS", "1213@347;ACEDININA"
444 DATA "1214@100;<I728-907", "1216?268;<ISTELLATIO", "1217@ 06;=IARKADIA"
445 DATA "1311= 0C;=NTREMOUS DEX", "1315P328;<IMIRRIAM", "1320?2 7K<ISAURUS"
446 DATA "1411@357;=NRANGENT", "1413M35A;>IDENOTAM", "1417@325;<IFICANT"
447 DATA "1511>328;@CTIONALE", "1515> 07;=ICALIT", "1519;395;DIASGARD"
448 DATA "1520@100;>ITAVONNI", "1611>3 8;<IPHLUME", "1711M 0A;<IEXTOLAY"
449 DATA "1719T35B=<ILANTH", "1811?38AK=IDINOM", "1815M 0A;=IGHANDI"
450 DATA "1817? 02<FIVICTORIA", "1912E2 9;=IDINOMN", "1916;2 4;FIYLAVEN"
451 DATA "1918?357;BISONTHERT", "1920M19D;>ID'GANZIO", "2011@269;BIWYPOC"
452 DATA "2111;100;FIDJINNI", "2112?1 6;AIRECH", "2212>258;=IK'KIRKA"
453 DATA "2215=36A;BIQUOPIST", "2311?398;<ITREECE", "2313>32A;<IECHISTE"
454 DATA "2314?325;=IPIREMA", "2317>38A;@IRHISE", "2319U35A;>IIVENDO"
455 DATA "2411F327;>IKEANOU", "2414>2 9;>ITUREDED", "2415@353C=IVREIBEFGER"
456 DATA "2416>324;=ILA'BELLE", "2417E 0B;=IEQUUS", "2418M357;=IICETINA"
457 DATA "2419>379;>ICOGRI", "2420N399;=ISKULL", "2512>359;=IKINORB"
458 DATA "2514>326;=IGILEDEN", "2519@269;>IPANNET", "2520E367;<IGARRINCSKI"
459 DATA "2612M8 E;=IMACENE", "2613<32D<<IFULACIN", "2620>279<AINATOKO"
460 DATA "2712L35A;=IRISEK", "2715<27B;=IPOROZLO", "2716T27F=<IRHYLANOR"
461 DATA "2720>327;=ILONESEDA", "2811@355;>IVALHALLA", "2812>24B;BIZIVIJE"
462 DATA "2814L188;>IJAE TELLONA", "2818;100;EIGEROME", "2912<2 B;>IHENOZ"
463 DATA "2913U359;=ICELEPINA", "2918=8 9;<IGITOSY", "3015=2 5;>IBELIZO"
464 DATA "3016@353;BIKEGENA", "3017@358;<IHERONI", "3019; 05;FI457-973"
465 DATA "3020> 0B;=ISOMEM", "3111=359;<IVINORIAN", "3112M328;>INUTEMA"
466 DATA "3114;100;DIHUDERU", "3118=1 6;>ICIPATWE", "3119>405C<IVANEJEN"
467 DATA "3212T32C;<IMARGESI", "3216G15A;BIBEVEY", "3218>19B;=ITACAXEB"
468 DATA "3220>2 5;>IPOWAZA", "0122?194;<NJUNCTION", "0129@357;<NUNIQUA"
469 DATA "0130<41A;@NGAROO", "0223=36B;=DSTERN-STERN", "0230@130;=N886-945"
470 DATA "0321F39A;>NNONYM", "0325E357;>DLABERV", "0326>369;>DEKTRON"
471 DATA "0421>28A;>DZAMINE", "0425>1 8;=DENGRANGE", "0426U 09;=DILIUM"
472 DATA "0427=4 9;<DROGET", "0429@336<<NKARDIN", "0430>2 5;<CBULARIA"
473 DATA "0526?3 3;=DRORRE", "0527T27C;<DMIRE", "0528@1 5;ANCONDARIA"
474 DATA "0530@368;<NDORANNIA", "0622?199;>DTERANT 340", "0624<39D;<DJACENT"
475 DATA "0625;100?DD494-908", "0627<27G<>DDARRIAN", "0721;336;DDTORMENT"
476 DATA "0723>2 9;<DTRIFUGE", "0724=36C;@DNOSEA", "0727F17A;>DSPUME"
477 DATA "0729?327<>NATOR", "0822@2 3;=DCUNNONIC", "0830=329;>CDEBARRE"
478 DATA "0921U409;>SHRUNTING", "0922U40A;>STIZON", "0927U28A;>SNARSIL"
479 DATA "0930<36B;<IFLAMMARION", "1022U3 B;>SCOLADA", "1026U40B;>SANDURIL"
480 DATA "1121U19A;>SMJOLNIR", "1123U4 A;ASJOYEUSE", "1126U23A;=SORCRIST"
481 DATA "1130H194;@SENOS", "1221E1 8;>SGUNGNIR", "1223T28C;=SGRAM"
482 DATA "1225U 0A;=SEXCALIBUR", "1324U 0A;=STYRFING", "1325T28C;=SSACNOTH"
483 DATA "1329E4 A;<ICALADBOLG", "1424U3 A;<SBEATER", "1429@328;AIGUNN"
484 DATA "1430@8 A;>ICALIBURN", "1522U 0A;=SDYRNWYN", "1523U4 B;>SDURENDAL"
485 DATA "1524U40A;=SHOFUD", "1525U1 A;=SSTING", "1526U2 A;=SBITER"
486 DATA "1529@100?>SSTEEL", "1626@100?>SIRON", "1627@ 00?<SBRONZE"
487 DATA "1628@100?=SMITHRIL", "1721>34C;<IARBA", "1727E35B;=IWARDN"
488 DATA "1728>327;<IOLYMPIA", "1729>219;=ISMOUG", "1822G 06;BIRABWHAR"
489 DATA "1824L 0B;AIADABICCI", "1825=8 B;=IZAIBON", "1826<1 E<<ITENALPHI"
490 DATA "1924@205;>IIANIC", "1927F408;>ISPIRELLE", "2024F 08;=IDERCHON"
491 DATA "2124T28D=<ILUNION", "2125E2 B;BISHIRENE", "2128?325;@IPENKWHAR"
492 DATA "2129>219;<IHARVOSETTE", "2224>329;=ICARSE", "2228= 0A;>IPERSEPHONE"
493 DATA "2321=328;>IQUIRU", "2322;322;EIGORRAM", "2323E32B;=IRESTEN"
494 DATA "2324M1 A;<ICAPON", "2325>32A;=ISHARRIP", "2327L28D;<ISTROUDEN"
495 DATA "2425@7 8;=IGANDR", "2426@258;=IDROLRAW", "2521=288;BIHERONI"
496 DATA "2523=3 6;>IBYRET", "2527@347;=IPIMANE", "2621<36A;<IFOSEY"
497 DATA "2624U2 8;=IMERCURY", "2627>358;=ITIVID", "2726>329;<ICAREY"
498 DATA "2728<39BCAIDUALE", "2824>199;@ICATUZ", "2827F325;>IMELETO"
499 DATA "2828M358;>IHEXOS", "2830F 07;=IPEDASE", "2924N328;=IMORAN"
500 DATA "2927< 0B;>IMAITZ", "2930F33A;=IMAINZ", "3021>357;>IBRODIE"
501 DATA "3022>32A;=IRORISE", "3024= 07;@IJOKOTRE", "3025<27C;=IFORNICE"
502 DATA "3026@327;=IGRILLE", "3029< 0E;@IPALIQUE", "3030F358;=INEXINE"
503 DATA "3123G177;<INADRIN", "3124T28F;>IMORA", "3223F 07;<IDOJODO"
504 DATA "3228>329;BIFENL'S GREN", "0133M2 B;AIEMAPE", "0139M17B;@IRAWEH"
505 DATA "0140@323;=N876-574", "0231@268;<NSAXE", "0236>1 9<EIANDOR"
506 DATA "0240@358;>N769-422", "0332@367;@IGOTHE", "0333= 0A;BIMIRRIAM"
507 DATA "0336>358<DICANDORY", "0340@354;<NWONDERAY", "0433M 09;@IJONE"
508 DATA "0440?347;ANJINX", "0532?2 7;<CUCELLA", "0533>204;>IPENELOPE"
509 DATA "0534T4 C;@IKARIN", "0538M1 7;@IWONSTAR", "0539>329C=IFROIN"
510 DATA "0632F1 7;>IGOHATURE", "0637> 09;@IQUHAIATHAT", "0638@357;AILAKOU"
511 DATA "0731@358;=NRALHE", "0732L4 C==IIDERATI", "0739@ 07;=NTONDOUL"
512 DATA "0834H357;@C875-496", "0837@2 7;<NOCHECATE", "0838?4 5;=NMEWEY"
513 DATA "0840@349;>N975-452", "0931=26A;<NASTELTINE", "0938?17A;>NINCHIN"
514 DATA "0940?396;=NSINGER", "1031@130;<N567-908", "1037>36A;>NAVASTAN"
515 DATA "1040> 0A;<NKUAI QING", "1131@355;<NFALDOR", "1132G7 9;>CBOWMAN"
516 DATA "1133>359;<NSQUALLIA", "1138=3 A;=CTARSUS", "1232F328;=CWALSTON"
517 DATA "1233@268;<NFLEXOS", "1237E28D;=CCOLLACE", "1238>406;ANPAVABID"
518 DATA "1331@358;=NDATRILLIAN", "1332;140;FNNIRTON", "1337@120C>CJUDICE"
519 DATA "1339=40C;>NTREXALON", "1340M3 5;<CMOTMOS", "1433@238;=NNOCTOCOL"
520 DATA "1434F3 7;@CTARKINE", "1435=409;<CDALLIA", "1436@369;<NTALOS"
521 DATA "1531@100;>NDAWNWORLD", "1532M358;=CELIXABETH", "1533? 0A;<NFORINE"
522 DATA "1537E40B;<IMERTACTOR", "1631>268;ACTALCHEK", "1632@ 07;<NMILAGRO"
523 DATA "1634>404;>NPAGATON", "1635<34A;<NBINGES", "1637U26C;=IMILLE FALCS"
524 DATA "1731<38B;>IGROTE", "1733@357;=ILYDIA", "1736?197;BIMELIOR"
525 DATA "1737M269;>IEGYPT", "1739>359;=IASTER", "1836@216;<ICALLIA"
526 DATA "1932>268;=IMITHRAS", "1934<35B;AIWEISS", "1935>359;<IWINDSOR"
527 DATA "1937=2 9;>IOVERNALE", "1938M 0B;=INEW ROME", "1939>355;>ICRAW"
528 DATA "2035=299;BIAKI", "2036T6 F;>IGLISTEN", "2038>35B<=ITRANE"
529 DATA "2132@367;<ICENTRY", "2134>365;<ICALEDONIA", "2137@3 2;>ISOREL"
530 DATA "2138F 0A;<IHOROSHO", "2140U198;>IROMAR", "2231G4 5?BIMARASTAN"
531 DATA "2232@277;BICROUT", "2233>24B;>ITIREM", "2234>38B;>IINTHE"
532 DATA "2236?197;=ITSARINA", "2237F32A;@IWURZBURG", "2331@352;<IBICORNN"
533 DATA "2334< 0C;AIFFUDN", "2336T2 C;<IBENDOR", "2534>378;=IBURTSON"
534 DATA "2536<38B;=ISQUANINE", "2537D19A;>IDOBHAM", "2538@322;>IPYRAMUS"
535 DATA "2539@196;BITHISBE", "2540= 06;>IARAMIS", "2637>8 C;AIROBIN"
536 DATA "2638@ 05;<ID'MARA", "2639>359;=IKELTCHNER", "2731=327;<ITUSSINIAN"
537 DATA "2733<40B;BIEDENELT", "2735G2 7;>ICONWAY", "2739F297;BIDODDS"
538 DATA "2832@325;=ILEANDER", "2833?2 3;=IPEPERNIUM", "2834=186<<ITRALTHA"
539 DATA "2839F357;=IFARQUAHAR", "2933@2 6;=IRAYDRAD", "2934=357;=IZYRA"
540 DATA "2935M356;=IMURCHISON", "2936<36B;BIHAMMERMIUM"
541 DATA "2940G358;=ITHORNNASTOR"
542 DATA "3032=36B;=IKATARULU", "3035=2 6;<IPRILISSA", "3038>359;=ITEE-TEE-TEE"
543 DATA "3039<32B;=IYOUGHAL", "3040G359;=ITENELPHI", "3138>2 5;AIZEPHYR"
544 DATA "3139E2 5;>ICHAMOIS", "3233= 08;>IRAMIVA", "3235T28F==ITRIN"
545 DATA "3236>1 5;@IHAZEL"
546 DATA 0
547 REM  CARGO PRICE PER TON
548 REM  (CA,UW$,TC$,FLUX) => PR
549 TL$ = RIGHT$(UW$,1)
550 TL = ASC(TL$)-48
551 IF TL > 16 THEN TL = TL - 7
552 PR = 100 + TL * 10
553 CT$ = CT$(CA)
554 PR = PR * VAL(MID$(CT$,16,1)) + FL * 10
555 IF LEFT$(CT$,2) = LEFT$(TC$,2) THEN PR = PR - 100
556 IF RIGHT$(CT$,2) = LEFT$(TC$,2) THEN PR = PR + 100 * VAL(MID$(CT$,17,1))
557 IF PR < 1 THEN PR = 10 :REM STOPGAP
558 RETURN
559 REM COMPOSE_HEX(C,R) -> C$,R$,LO$
560 C$ = "0" + MID$(STR$(C),2)
561 R$ = MID$(STR$(R),2)
562 IF R < 10 THEN R$ = "0" + R$
563 LO$ = C$ + R$
564 RETURN
565 REM COMPUTE-LOC(C,R) -> C$,R$,LO$
566 C$ = MID$(STR$(C),2)
567 IF C < 10 THEN C$ = "0" + C$
568 R$ = MID$(STR$(R),2)
569 IF R < 10 THEN R$ = "0" + R$
570 LO$ = C$ + R$
571 RETURN
572 REM IN$ HAS "ORDERED" UPP
573 REM UP$ HAS CANONICAL UPP
574 FOR X = 1 TO 6
575 VA = 10 - X
576 IF MID$(IN$,X,1) = "S" THEN UU(1) = VA
577 IF MID$(IN$,X,1) = "D" THEN UU(2) = VA
578 IF MID$(IN$,X,1) = "E" THEN UU(3) = VA
579 IF MID$(IN$,X,1) = "I" THEN UU(4) = VA
580 IF MID$(IN$,X,1) = "U" THEN UU(5) = VA
581 IF MID$(IN$,X,1) = "C" THEN UU(6) = VA
582 NEXT
583 UP$ = ""
584 FOR X = 1 TO 6
585 UP$ = UP$ + MID$(STR$(UU(X)),2)
586 NEXT
587 RETURN
588 REM
589 REM  DECODE SY$ INTO ITS COMPONENT PARTS:
590 REM
591 REM  D6323312TTGNEAEGCOR
592 REM      1: STARPORT + BASES
593 REM    2-3: TC INDEX
594 REM      4: TL
595 REM      5: TC1 AN,CP,RE,RS,SA
596 REM      6: BELT, GG, AND ZONE INDICATORS
597 REM      7: ALLEGIANCE
598 REM     8+: NAME
599 REM
600 SP=ASC(LEFT$(SY$,1))-59
601 BA$=" "
602 IF SP AND 8  =  8 THEN BA$ = "S"
603 IF SP AND 16 = 16 THEN BA$ = "N"
604 IF SP AND 24 = 24 THEN BA$ = "2"
605 SP = SP AND 7
606 SP$ = CHR$(64+SP)
607 IF SP = 0 THEN SP$ = "X"
608 UW$ = SP$ + "-" + MID$(SY$,4,1)
609 ZO = ASC(MID$(SY$,6,1))-59
610 BE = 0: GG = 0: ZO$ = " "
611 IF ZO AND 1 = 1 THEN BE = 1
612 IF ZO AND 2 = 2 THEN GG = 1
613 IF ZO AND 4 = 4 THEN ZO$ = "A"
614 IF ZO AND 8 = 8 THEN ZO$ = "R"
615 AL$ = MID$(SY$,7,1)
616 TX = VAL(MID$(SY$,2,2))
617 TC$ = TC$(TX)
618 T1 = ASC(MID$(SY$,5,1))-59
619 IF (T1 AND 1)  = 1  THEN TC$ = TC$ + " AN"
620 IF (T1 AND 2)  = 2  THEN TC$ = TC$ + " CP"
621 IF (T1 AND 4)  = 4  THEN TC$ = TC$ + " RE"
622 IF (T1 AND 8)  = 8  THEN TC$ = TC$ + " RS"
623 IF (T1 AND 16) = 16 THEN TC$ = TC$ + " SA"
624 NA$ = MID$(SY$,8)
625 RETURN
626 REM DISTANCE(C,R,C1,R1) -> D%
627 A1 = R + INT(C/2)
628 A2 = R1 + INT(C1/2)
629 D1% = ABS(A1-A2)
630 D2% = ABS(C-C1)
631 D3% = ABS( (A1-C) - (A2-C1) )
632 D% = D3%
633 IF (D1% >= D2%) AND (D1% >= D3%) THEN D% = D1%
634 IF (D2% >= D1%) AND (D2% >= D3%) THEN D% = D2%
635 RETURN
636 REM JUMP-MAP(C,R,JR) -> MA$()
637 FOR NN=0 TO 24 : MA$(NN) = "" : NEXT
638 NN%=0
639 FOR C1 = C-JR TO C+JR : IF C1 < 0 OR C1 > 32 THEN NEXT
640 FOR R1 = R-JR TO R+JR : IF R1 < 0 OR R1 > 40 THEN NEXT
641 IF NN% = 24 THEN 656
642 REM TOO CLOSE/FAR?
643 GOSUB 626: IF (D% > JR) GOTO 656
644 REM EMPTY HEX?
645 IF SY$(C1,R1) = "" GOTO 656
646 SY$ = SY$(C1,R1):GOSUB 588:REM PARSE SY$
647 X$ = MID$(STR$(C1),2)
648 Y$ = MID$(STR$(R1),2)
649 IF C1 < 10 THEN X$ = "0" + X$
650 IF R1 < 10 THEN Y$ = "0" + Y$
651 NN% = NN% + 1
652 NA$=LEFT$(NA$+"                    ",15)
653 TC$=LEFT$(TC$+"                    ",15)
654 D$=MID$(STR$(D%),2)
655 MA$(NN%) = X$+Y$+" ("+D$+") "+NA$+" "+TC$
656 NEXT:NEXT
657 SY$ = SY$(C,R): REM CLEAN UP!
658 RETURN
659 REM C1,R1,D%,JR -> C,R,SY$
660 REM IF D%>JR, NO GO
661 IF (D% > JR) THEN RETURN
662 C = C1: R = R1
663 SY$ = SY$(C,R)
664 RETURN
665 REM SHOW CARGO PRICES
666 ?:? "CARGO PRICES PER TON:"
667 FOR CA = 0 TO 10
668 GOSUB 588 :REM UW$, TC$
669 GOSUB 547
670 PR$ = LEFT$(MID$(STR$(PR)+"                    ",2),5)
671 ? CA, MID$(CT$(CA),4,12) "CR " PR$ " (" CA(CA) "T)"
672 CP(CA) = PR
673 NEXT
674 RETURN
