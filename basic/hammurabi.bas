10 	SC = 32 :REM SCREEN INDENT
20 	COLOR 8,0 :? CHR$(147) "\X11" 
30 	? TAB(SC) "HAMMURABI"
40 	? TAB(SC/2) "\X11\X11CREATIVE COMPUTING   MORRISTOWN, NEW JERSEY   1978"
50 	? "\X11\X11TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA"
60 	? "FOR A TWELVE-YEAR TERM OF OFFICE." 
70 	INPUT "\X11\X11PRESS <ENTER> TO BEGIN"; E$
80 	X=RND(-TI) :REM SRAND
90 	D1=0   :REM TOTAL DEATHS
100 	P1=0   :REM STARVATIONS PER YEAR
110 	Z=0    :REM CURRENT YEAR
120 	P=95   :REM POPULATION
130 	S=2800 :REM STORES OF GRAIN
140 	H=3000
150 	E=H-S  :REM RAT DEPREDATIONS
160 	Y=3    :REM YIELD PER ACRE
170 	A=H/Y  :REM ACRES OF FARMLAND
180 	I=5    :REM IMMIGRATED
190 	Q=1    :REM PLAGUE (FOR SUMMARY USE)
200 	D=0    :REM DIED
210 	Z=Z+1
220 	? "\X11\X11\X11HAMMURABI: I BEG TO REPORT TO YOU, IN YEAR" Z ",\X11"
230 	? TAB(SC/2) D "PEOPLE STARVED."
240 	? TAB(SC/2) I "PEOPLE CAME TO THE CITY.\X11"
250 	P=P+I
260 	IF Q>0 GOTO 290
270 	P=INT(P/2)
280 	? TAB(SC/2) "A HORRIBLE PLAGUE STRUCK! HALF THE POPULATION DIED!\X11"
290 	? TAB(SC/2) "CURRENT POPULATION        :" P
300 	? TAB(SC/2) "ACRES OWNED BY THE CITY   :" A
310 	? TAB(SC/2) "BUSHELS HARVESTED PER ACRE:" Y
320 	? TAB(SC/2) "BUSHELS EATEN BY RATS     :" E
330 	? TAB(SC/2) "BUSHELS IN STORE          :" S
340 	IF Z=13 GOTO 1090
350 	C = INT(10*RND(1)): Y=C+17
360 	? "\X11LAND IS TRADING AT" Y "BUSHELS PER ACRE.\X11"
370 	INPUT "HOW MANY ACRES DO YOU WISH TO BUY"; Q$
380 	Q=VAL(Q$)
390 	IF Q<0 GOTO 1020
400 	IF Q=0 GOTO 470
410 	IF Y*Q <= S GOTO 440
420 	GOSUB 1050
430 	GOTO 370
440 	A=A+Q :S=S-Y*Q :C=0
450 	?
460 	GOTO 560
470 	INPUT "HOW MANY ACRES DO YOU WISH TO SELL"; Q$
480 	Q=VAL(Q$)
490 	IF Q<0 GOTO 1020
500 	IF Q=0 THEN ? :GOTO 560
510 	IF Q<A GOTO 540
520 	GOSUB 1070
530 	GOTO 470
540 	A=A-Q :S=S+Y*Q :C=0
550 	?
560 	INPUT "HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE"; Q$
570 	Q=VAL(Q$)
580 	IF Q<0 GOTO 1020
590 	IF Q=0 GOTO 650
600 	IF Q <= S GOTO 630
610 	GOSUB 1050
620 	GOTO 560	
630 	S=S-Q :C=1
640 	?
650 	INPUT "HOW MANY ACRES DO YOU WISH TO PLANT WITH SEED"; D$
660 	D=VAL(D$)
670 	IF D=0 GOTO 790
680 	IF D<0 GOTO 1020
690 	IF D<=A GOTO 720
700 	GOSUB 1070
710 	GOTO 650
720 	IF INT(D/2) <= S GOTO 750
730 	GOSUB 1050
740 	GOTO 650
750 	IF D<=10*P GOTO 780
760 	? "BUT YOU HAVE ONLY" P "PEOPLE TO TEND THE FIELDS!  NOW THEN,"
770   	GOTO 650
780 	S=S-INT(D/2)
790 	GOSUB 1000
800 	REM A BOUNTIFUL HARVEST!
810 	Y=C :H=D*Y :E=0
820 	GOSUB 1000
830 	IF INT(C/2)<>C/2 GOTO 860
840 	REM RATS RUN WILD!
850 	E=INT(S/C) 
860 	S=S-E+H	
870 	GOSUB 1000
880 	REM POPULATION INCREASE
890 	I = INT(C*(20*A+S)/P/100+1)
900 	REM CHECK HUNGER LEVELS
910 	C=INT(Q/20)
920 	REM 15% CHANCE OF PLAGUE
930 	Q=INT(10*(2*RND(1)-.3))
940 	IF P<C THEN 200	
950 	REM STARVE ENOUGH FOR IMPEACHMENT
960 	D=P-C :IF D>.45*P GOTO 1280
970 	P1=((X-1)*P1+D*100/P)/Z
980 	P=C :D1=D1+D 
990 	GOTO 210
1000 	C = INT(RND(1)*5)+1
1010 	RETURN
1020 	? "HAMMURABI: I CANNOT DO WHAT YOU WISH."
1030 	? "GET YOURSELF ANOTHER STEWARD!!!!!"
1040 	GOTO 1420
1050 	? "HAMMURABI: THINK AGAIN. YOU HAVE ONLY" S "BUSHELS OF GRAIN. NOW THEN,"
1060 	RETURN
1070 	? "HAMMURABI: THINK AGAIN. YOU OWN ONLY" A "ACRES. NOW THEN,"
1080 	RETURN
1090         IF D1 > 0 THEN 1120
1100         ? "\X11IN YOUR TERM OF OFFICE, NO-ONE DIED FROM STARVATION."
1110         GOTO 1150
1120 	? "\X11IN YOUR TERM OF OFFICE," P1 "PERCENT OF THE"
1130 	? "POPULATION STARVED PER YEAR ON THE AVERAGE, I.E. A TOTAL OF"
1140 	? D1 "PEOPLE DIED!!" 
1150 	L% = A/P
1160 	? "\X11YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
1170 	? L% "ACRES PER PERSON."
1180 	?
1190 	IF P1>33 GOTO 1300
1200 	IF L<7   GOTO 1300
1210 	IF P1>10 GOTO 1340
1220 	IF L<9   GOTO 1340
1230 	IF P1>3  GOTO 1380
1240 	IF L<10  GOTO 1380
1250 	? "A FANTASTIC PERFORMANCE!!! CHARLEMAGNE, DISRAELI, AND"
1260 	? "JEFFERSON COMBINED COULD NOT HAVE DONE BETTER!"
1270 	GOTO 1420
1280 	?
1290 	? "YOU STARVED" D "PEOPLE IN ONE YEAR!!!"
1300 	? "DUE TO THIS EXTREME MISMANAGEMENT YOU HAVE NOT ONLY"
1310 	? "BEEN IMPEACHED AND THROWN OUT OF OFFICE BUT YOU HAVE"
1320 	? "ALSO BEEN DECLARED NATIONAL FINK!!!!"
1330 	GOTO 1420
1340 	? "YOUR HEAVY-HANDED PERFORMANCE SMACKS OF NERO AND IVAN IV."
1350 	? "THE PEOPLE (REMAINING) FIND YOU AN UNPLEASANT RULER, AND,"
1360 	? "FRANKLY, HATE YOUR GUTS!!" 
1370 	GOTO 1420
1380 	? "YOUR PERFORMANCE COULD HAVE BEEN SOMEWHAT BETTER, BUT"
1390 	? "REALLY WASN'T TOO BAD AT ALL. " INT(P*.8*RND(1)) "PEOPLE"
1400 	? "DEARLY LIKE TO SEE YOU ASSASSINATED BUT WE ALL HAVE OUR"
1410 	? "TRIVIAL PROBLEMS."
1420 	? "SO LONG FOR NOW."
1430 	?
1440 	END
