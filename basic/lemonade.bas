5 REM LEMONADE STAND FOR X16, EDIT 2021 ROBERT EAGLESTONE
10 REM BORING NOTES AT THE END OF THIS LISTING
15 DIM A0$(30)
20 A0$(0)="\X13"
25 FORX=1TO30:A0$(X)=A0$(X-1)+"\X11":NEXT
30 REM ------------------------------------
35 REM
40 REM
45 REM  COMMODORE TITLE PAGE (CA. 1986)
50 REM
55 REM
60 REM ------------------------------------
65 SCREEN 0  :REM X16 40X25 MODE
70 COLOR 5,0 :REM X16 GREEN-ON-BLACK
75 ?CHR$(147)
80 PRINT"\X93\X11\X1D";
85 FORI=1TO38:PRINT"\X12Š\X92";:NEXT
90 FORI=1TO27:PRINT"\X11\X9D\X12Š\X92";:NEXT
95 FORI=1TO37:PRINT"\X9D\X9D\X12Š\X92";:NEXT
100 FORI=1TO27:PRINT"\X91\X9D\X12Š\X92";:NEXT
105 PRINT"\X13\X11\X11\X11\X11\X11\X11\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12©  \X92
110 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12 \X92  \X12 \X92©
115 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12 \X92  \X12£ß
120 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1Dß\X12  \X92
125 PRINT"\X11\X11\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DC O M M O D O R E\X11\X11
130 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DEDUCATIONAL\X11\X11
135 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DSOFTWARE
140 ? A0$(24);"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12PRESS SPACE BAR OR WAIT\X92":I=0
145 TI$="000000"
150 GETA$:IFA$<>" "THENIFVAL(TI$)<7THEN 150
155 GOSUB 2045 :REM INTRO
160 V=RND(-TI) :REM SEED RANDOMIZER
165 ?"\X93\X11\X11 LEMONADE STAND \X11\X11"
170 INPUT" WHAT IS YOUR NAME";N$
175 IF N$="" GOTO 65 
180 N$=LEFT$(N$,13)
185 ?
190 ?:?" " N$", YOU SHALL OPERATE YOUR"
195 ?:?" STAND FOR 12 WEEKS."
200 GOSUB 1670 :REM SPACEBAR
205 REM ------------------------------------
210 REM
215 REM
220 REM  INITIALIZATION
225 REM
230 REM
235 REM ------------------------------------
240 WK=12 :REM # OF WEEKS TO RUN
245 B=10  :REM STARTING CASH
250 D1=0  :REM STARTING LEMON CONCENTRATE
255 A1=0  :REM STARTING SUGAR
260 P1=0  :REM STARTING CUPS
265 X=1
270 REM ------------------------------------
275 REM
280 REM
285 REM  MAIN LOOP
290 REM
295 REM
300 REM ------------------------------------
305 FX=0
310 ? "\X93\X11";
315 ?" WEEK NUMBER" X
320 ? "
325 ? "                       YOU HAVE:
330 ? "
335 ? "    §£££££££££££££££¥         QTS/CONC
340 ? "    §               ¥         LBS/SUGAR
345 TA=4+INT(((14-LEN(N$))/2)+.5)
350 ? TAB(TA)"\X91"N$"'S"
355 ? "    §   LEMONADE    ¥         CUPS
360 ? "    §               ¥         
365 ? "    §        CENTS  ¥  WEATHER:
370 ? "    §£££££££££££££££¥  
375 ? "    § €€€           ¥  
380 ? "    § Í ÎÉ          ¥
385 ? "    § ( )Ë¡¡¡¡¡¡¡¡¡ ¥
390 ? "    §\X12©             ß\X92¥
395 ? "    \X12©               ß\X92
400 ? "    È(Ù@Ç()ÕÉÇ()Ç)Ç*Ç
405 ? "    È)Ù(Â)(ÊËÂ)(Â@Ç)Ç
410 ? "    È(Ù)Ç(×()Ç(*Ç)Ç(Ç
415 ? "    È)Ù(Â)(@(Â)(Â(Ç)Ç
420 ? "    È*Ù)Ç*)()Ç()Ç)Ç(Ç
425 ? "    È)Ù(Ç)()(Ç)(Ç(Ç)Ç
430 ? "±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
435 GOSUB 1565 :REM SHOW VALUES
440 REM ------------------------------------
445 REM
450 REM  PREDICTIONS
455 REM
460 REM  T: TEMP
465 REM  H: HUMIDITY
470 REM  S: COST OF SUGAR
475 REM  L: COST OF LEMON CONCENTRATE
480 REM  C: COST OF CUPS
485 REM  Z: TEMP VARIATION (ACTUAL)
490 REM  T2: FELT (LOW) TEMPERATURE W/HUMIDITY
495 REM  F2,F4: THRESHOLD VALUES
500 REM
505 REM ------------------------------------
510 T=(INT(RND(1)*3)+7)*10
515 H=RND(1)
520 IF H>.66 THEN H$="HIGH"
525 IF H<.33 THEN H$="LOW"
530 IF H>.32 AND H<.67 THEN H$="AVERAGE"
535 S=.99+(.01)*INT(RND(1)*9):REM**COST OF SUGAR
540 L=.69+(.01)*INT(RND(1)*9):REM**COST OF CONCENTRATE
545 C=.90+(.01)*INT(RND(1)*9):REM**COST OF CUPS
550 Z=-5+INT(RND(1)*20)
555 T2=T+Z-INT(H*10)
560 F2=INT(RND(1)*30)+70
565 F4=(((T+Z+T2)-137.5)/25)
570 IF F4<.1 THEN F4=.1
575 IF F4>1  THEN F4=1
580 GOSUB 1840 :REM CLEAR W
585 ? A0$(11) TAB(23) "PREDICTION FOR"
590 ? A0$(12) TAB(23) "SATURDAY:"
595 ? A0$(14) TAB(23) "TEMPERATURES IN"
600 ? A0$(15) TAB(23) "THE"T"\X9D'S AND"
605 ? A0$(16) TAB(23)  H$" HUMIDITY"
610 REM ------------------------------------
615 REM 
620 REM 
625 REM  BUY LEMON CONCENTRATE
630 REM 
635 REM 
640 REM ------------------------------------
645 GOSUB 1565 :REM SHOW VALUES
650 GOSUB 1895 :REM CLEAR D
655 ? A0$(23) "CONC. LEMON IS $"L"A QUART."
660 ? A0$(24) "HOW MANY QUARTS DO YOU WANT TO BUY? ";
665 GOSUB 1950 :D=AN 
670 IF D*L<B GOTO 695
675 ? "YOU DON'T HAVE ENOUGH MONEY"
680 ? "FOR "D" BOTTLES."
685 GOSUB 1670 :REM SPACEBAR
690 GOTO 645
695 B=B-(D*L) :D1=D1+D
700 GOSUB 1565 :REM SHOW VALUES
705 IF FX=2 GOTO 955 :REM CHANGE
710 REM ------------------------------------
715 REM 
720 REM 
725 REM  BUY SUGAR
730 REM 
735 REM 
740 REM ------------------------------------
745 GOSUB 1565 :REM SHOW VALUES
750 GOSUB 1895 :REM CLEAR D
755 ? A0$(23) "SUGAR IS $"S"FOR A 5-LB BAG."
760 ? A0$(24) "HOW MANY BAGS DO YOU WANT TO BUY? ";
765 GOSUB 1950 :A=AN
770 IF A*S<B GOTO 795
775 ? A0$(25) "YOU DON'T HAVE ENOUGH MONEY"
780 ? A0$(26) "FOR" A "BAGS."
785 GOSUB 1670 :REM SPACEBAR
790 GOTO 745
795 B=B-(A*S) :A1=A1+(5*A)
800 GOSUB 1565 :REM SHOW VALUES
805 IF FX=2 GOTO 955 :REM CHANGE
810 REM ------------------------------------
815 REM 
820 REM 
825 REM  BUY CUPS
830 REM 
835 REM 
840 REM ------------------------------------
845 GOSUB 1895 :REM CLEAR D
850 ? A0$(23) "CUPS ARE $"C"FOR A BAG OF 100."
855 ? A0$(24) "HOW MANY BAGS DO YOU WANT TO BUY? ";
860 GOSUB 1950 :P=AN
865 IF P*C<B GOTO 885
870 ? "YOU DON'T HAVE ENOUGH MONEY"
875 ? "FOR"P"BAGS."
880 GOTO 845
885 B=B-(P*C):P1=P1+(P*100)
890 GOSUB 1565 :REM SHOW VALUES
895 IF FX=2 GOTO 955 :REM CHANGE
900 REM ------------------------------------
905 REM
910 REM
915 REM  CLEAR TEMPERATURE PREDICTION
920 REM  AND REPLACE WITH ACTUALS.
925 REM
930 REM
935 REM ------------------------------------
940 GOSUB 1840 :REM CLEAR W
945 ? A0$(12) TAB(23) "TEMPERATURE:"Z+T
950 ? A0$(13) TAB(23) "WET BULB   :"T2
955 GOSUB 1895 :REM CLEAR D
960 ? A0$(23) "DO YOU WANT TO CHANGE ANY"
965 ? "OF THE ITEMS? [YN]";
970 GET YN$: IF YN$="N" THEN 1025
975 IF YN$<>"Y" GOTO 970
980 FX=2
985 GOSUB 1895 :REM CLEAR D
990 ? A0$(23) "WHICH ITEM: LEMON=1   SUGAR=2   CUPS=3"
995 NI$=""
1000 GET NI$
1005 IF NI$="1" THEN B=B+(A*S) :D1=D1-D       :GOTO 645
1010 IF NI$="2" THEN B=B+(D*L) :A1=A1-(5*A)   :GOTO 745
1015 IF NI$="3" THEN B=B+(P*C) :P1=P1-(P*100) :GOTO 845
1020 GOTO 1000
1025 GOSUB 1895 :REM CLEAR D
1030 ? A0$(23) "PRICE (IN CENTS) FOR A CUP"
1035 ? "OF LEMONADE: ";
1040 GOSUB 1950 :K=AN
1045 IF K<1 OR K>80 GOTO 1025
1050 F3=3-((K/3)/((((T+Z+T2)-37.5)^3)/10^6))
1055 IF F3<0 THEN F3=0
1060 GOSUB 1895 :REM CLEAR D
1065 ? A0$(9) TAB(9) K :REM PRICE PER CUP
1070 F1=INT(F2*F3*F4)
1075 IF F1<0 THEN F1=0
1080 IF F1<1 THEN W=1: IF F1<1 GOTO 1250
1085 REM ------------------------------------
1090 REM 
1095 REM 
1100 REM  RUN THE SIMULATION F1 TIMES
1105 REM
1110 REM 
1115 REM ------------------------------------
1120 FOR W=1 TO F1
1125    IF P1<.01 THENP1=0
1130    IF D1<.01 THEND1=0
1135    IF A1<.01 THENA1=0
1140    IF P1=0 OR A1=0 OR D1=0 GOTO 1175
1145    GOSUB 1565 :REM SHOW VALUES
1150    D1=D1-(1/40)
1155    A1=A1-(1/20)
1160    P1=P1-1
1165    B=B+(K*.01)
1170 NEXT W
1175 GOSUB 1565 :REM SHOW VALUES
1180 REM ------------------------------------
1185 REM 
1190 REM 
1195 REM  DISPLAY RESULTS AND COMMENTARY
1200 REM
1205 REM 
1210 REM ------------------------------------
1215 IF W=F1 GOTO 1250
1220 MM$="CUPS."
1225 IF P1<=0 THEN GOSUB 1775
1230 MM$="LEMON JUICE."
1235 IF D1<=0 THEN GOSUB 1775
1240 MM$="SUGAR."
1245 IF A1<=0 THEN GOSUB 1775
1250 GOSUB 1895 :REM CLEAR D
1255 ? A0$(23) "YOU MADE"W-1"SALES AT"K"CENTS A CUP."
1260 ? A0$(24) "YOU GROSSED $"(W-1)*(K/100)"\X9D."
1265 FOR K1=1 TO 50
1270    F5=3-((K1/3)/((((T+Z+T2)-37.5)^3/10^6)))
1275    G5=(INT(F5*F2*F4))*(K1/100)
1280    IF G5<G9 GOTO 1295
1285    G9=G5
1290    K9=K1
1295 NEXTK1
1300 IF K9=K GOTO 1320 :REM BEST PRICE
1305 ? A0$(25) "YOU COULD HAVE GROSSED $"G9"TODAY,"
1310 ? A0$(26) "IF YOU HAD CHARGED"K9"CENTS."
1315 GOTO 1335
1320 ? A0$(25)  K "CENTS WAS THE BEST PRICE FOR TODAY."
1325 IF FX<>1 THEN ? A0$(26) "YOU'RE A GOOD BUSINESSPERSON."
1330 COUNT=COUNT+1
1335 K9=0:G9=0
1340 GOSUB 1720 :REM WAIT
1345 GOSUB 1670 :REM SPACEBAR
1350 X=X+1: IF X<=WK GOTO 305 :REM LOOP
1355 REM ------------------------------------
1360 REM 
1365 REM 
1370 REM  END / SUMMARY
1375 REM
1380 REM 
1385 REM ------------------------------------
1390 ?CHR$(147)
1395 ? "\X12SUMMER'S END\X92"
1400 ?:? " THE SUMMER IS OVER."
1405 ?:? " YOU HAVE $"B"."
1410 ?:? " YOU OWE YOUR FAMILY $10, SO"
1415 ?:? " YOU EARNED $"B-10"THIS SUMMER."
1420 ?:? " YOU HAVE"P1"CUPS,"INT(D1)"BOTTLES OF"
1425 ?:? " LEMON JUICE, AND"INT(A1)"LBS OF"
1430 ?:? " SUGAR LEFT OVER."
1435 IF COUNT<WK/2 GOTO 1470
1440 ?
1445 ?:? " " N$ ", YOU ARE TRULY A GIFTED BUSINESS-"
1450 ?:? " PERSON. "; INT(COUNT/(X-1)*100);"% OF YOUR PRICES"
1455 ?:? " WERE THE BEST POSSIBLE!! "
1460 ?:? " THE ROCKEFELLERS AND GETTYS OF THE"
1465 ?:? " WORLD SALUTE YOU."
1470 GOSUB 1670
1475 SCREEN $02 :REM X16 BACK TO 80X60
1480 END
1485 REM ------------------------------------
1490 REM
1495 REM
1500 REM
1505 REM       SUBROUTINES BEGIN HERE
1510 REM
1515 REM
1520 REM
1525 REM ------------------------------------
1530 REM ------------------------------------
1535 REM
1540 REM
1545 REM  SHOW VALUES (OLD LINE 3130)
1550 REM
1555 REM
1560 REM ------------------------------------
1565 FORU=1TO6
1570    IF MID$(STR$(B),U,1)="."GOTO 1585
1575 NEXT U
1580 GOTO 1595
1585 B=VAL(LEFT$(STR$(B),U+2))
1590 B=INT(B*100)/100
1595 U6=ABS(B)+1
1600 U7=INT(U6)
1605 U6$=STR$(U7-1)+"."+MID$(STR$(U6)+"00000",LEN(STR$(U7))+2,2)
1610 ? A0$(4) TAB(23)"          \X9D\X9D\X9D\X9D\X9D\X9D\X9D$" U6$
1615 ? A0$(5) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" INT(D1)
1620 ? A0$(6) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" INT(A1)
1625 ? A0$(7) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" P1
1630 RETURN
1635 REM ------------------------------------
1640 REM
1645 REM
1650 REM  PRESS SPACE TO CONTINUE
1655 REM 
1660 REM
1665 REM ------------------------------------
1670 ? A0$(27) "\X1D\X1D\X1D\X1D\X1D\X1D\X12 PRESS SPACEBAR TO CONTINUE "
1675 GETA$:IFA$<>" "THEN 1675
1680 RETURN
1685 REM ------------------------------------
1690 REM
1695 REM
1700 REM  A SHORT WAIT (500 JIFFYS)
1705 REM
1710 REM
1715 REM ------------------------------------
1720 TT=TI+500
1725 IF TT>TI GOTO 1725
1730 RETURN
1735 REM ------------------------------------
1740 REM
1745 REM
1750 REM  YOU WOULD HAVE MADE <F1> SALES, 
1755 REM           IF YOU HAD MORE <MM$>.
1760 REM
1765 REM
1770 REM ------------------------------------
1775 GOSUB 1895 :REM CLEAR D
1780 ? A0$(23) "YOU WOULD HAVE MADE"F1"SALES,"
1785 ? A0$(24) "IF YOU HAD MORE " MM$
1790 FX=1
1795 GOSUB 1720 :REM WAIT
1800 RETURN
1805 REM ------------------------------------
1810 REM
1815 REM
1820 REM  CLEAR WEATHERBOARD
1825 REM
1830 REM
1835 REM ------------------------------------
1840 FOR V=10 TO 18
1845 ? A0$(V) TAB(23) "                "
1850 NEXT V
1855 RETURN
1860 REM ------------------------------------
1865 REM
1870 REM
1875 REM  CLEAR DIRECTIVES
1880 REM
1885 REM
1890 REM ------------------------------------
1895 FOR V=23 TO 28
1900 ? A0$(V) "                                       "
1905 NEXT V
1910 RETURN
1915 REM ------------------------------------
1920 REM
1925 REM
1930 REM  DIGIT COLLECTION
1935 REM
1940 REM
1945 REM ------------------------------------
1950 GET A1$ :IF A1$<"0" OR A1$>"9" GOTO 1950
1955 ? A1$;
1960 GET A2$ :IF A2$="" GOTO 1960
1965 IF A2$=CHR$(20) THEN ? CHR$(20); :GOTO 1950
1970 IF A2$=CHR$(13) THEN A2$="" :GOTO 2000
1975 IF A2$<"0" OR A2$>"9" THEN 1960
1980 ? A2$;
1985 GET A3$ :IF A3$="" GOTO 1985
1990 IF A3$=CHR$(20) THEN ? CHR$(20); :GOTO 1960
1995 IF A3$<>CHR$(13) GOTO 1985
2000 AN=VAL(A1$+A2$)
2005 RETURN
2010 REM ------------------------------------
2015 REM
2020 REM
2025 REM  INTRODUCTION
2030 REM
2035 REM
2040 REM ------------------------------------
2045 ?CHR$(147);
2050 ? CHR$(14);
2055 ? " \X12ÌEMONADE STAND\X92"
2060 ?
2065 ? " ÙOU'VE DECIDED TO RUN A LEMONADE STAND"
2070 ? " FOR 10 SATURDAYS THIS SUMMER IN ORDER "
2075 ? " TO MAKE SOME MONEY. ÂUT YOU SOON FIND "
2080 ? " THAT RUNNING A STAND COSTS MONEY."
2085 ? " ÙOUR JOB IS TO MAKE AS BIG A PROFIT AS"
2090 ? " YOU CAN."
2095 ?
2100 ? " ÙOUR FAMILY AGREES TO STAKE YOU $10 IN"
2105 ? " ORDER TO GET STARTED. ÁT THE END OF "
2110 ? " THE SUMMER YOU WILL HAVE TO RETURN THE"
2115 ? " $10, BUT YOU WILL NOT NEED TO PAY"
2120 ? " INTEREST."
2125 ?
2130 ? " ÉF YOU KEEP YOUR PRICE LOW, YOU'LL SELL";
2135 ? " MORE LEMONADE, BUT WILL MAKE LESS MONEY";
2140 ? " ON EACH SALE."
2145 ? " ÏN HOT OR HUMID DAYS, YOU WILL SELL"
2150 ? " MORE LEMONADE AND ALSO BE ABLE TO"
2155 ? " CHARGE MORE PER CUP."
2160 ? " ×HEN THE WET BULB READING IS CLOSE TO"
2165 ? " THE TEMPERATURE, IT IS A HUMID DAY."
2170 GOSUB 1720 :REM WAIT
2175 GOSUB 1670 :REM SPACEBAR
2180 ?CHR$(147):? CHR$(142)
2185 RETURN
2190 "-----------------------------------------------------
2195 :
2200 :
2205 "LEMONADE STAND DESIGN NOTES, R.EAGLESTONE 2021
2210 :
2215 :
2220 "-----------------------------------------------------
2225 "MY GOAL IS TO RE-CREATE THE ORIGINAL PET EXPERIENCE,
2230 "BUT ON THE X16 WITH A 40 X 25 DISPLAY.  I GO SO FAR AS 
2235 "TO USE THE ORIGINAL STARTING CASH AMOUNT, ORIGINAL 
2240 "IMPERIAL UNITS, AND A GREEN-ON-BLACK DISPLAY.
2245 :
2250 "THERE ARE A VERY FEW X16-SPECIFIC COMMANDS USED:
2255 "'COLOR' AND 'SCREEN' TO BE EXACT.
2260 :
2265 "WHAT IS UNCHANGED ARE THE ALGORITHMS USED TO 
2270 "CALCULATE THE PRICES, TEMPERATURES, AND DEMAND --
2275 "I.E. THE CORE MECHANICS OF THE GAME ITSELF.
2280 :
2285 "EFFORTS TO FIND THE SOURCE RETURNED TWO LATE VARIANTS:
2290 "(1) A VIC-20 LISTING MADE IN 2009
2295 "(2) THE C64 VERSION FROM 1986
2300 :
2305 "THE VIC-20 VERSION IS GREATLY STRIPPED DOWN, WHICH
2310 "ACTUALLY WAS A BOON SINCE IT LAID BARE THE FUNDAMENTAL
2315 "STRUCTURE OF THE GAME.
2320 :
2325 "THE C64 VERSION IS ABOUT 50% BOILERPLATE AND 
2330 "FRIVOLOUS POKES, INCLUDING SOME ML TO PREVENT THE
2335 "CODE FROM BEING MODIFIED.  AT THE SAME TIME, THAT
2340 "VERSION NOTES THAT THE PROGRAM WAS IN THE PUBLIC
2345 "DOMAIN. ITS CODE IS MORE FAITHFUL TO THE ORIGINAL,
2350 "AS WELL, ALTHOUGH DIFFERENCES EXIST.
2355 :
2360 "THIS VERSION WAS REBUILT USING THE VIC-20 VERSION
2365 "TO RECAPTURE THE ORIGINAL FLOW, THEN PASTING THE
2370 "ACTUAL 1986 LOGIC INTO THE FRAMEWORK.
2375 :
2380 "SUBROUTINES ARE NOT EQUIVALENT TO THE ORIGINALS.
2385 "IN FACT, EVEN THE INTERFACE IS NOT THE SAME, SINCE
2390 "I USED THE ORIGINAL PET VIEW AS THE TEMPLATE,
2395 "RATHER THAN THE C64/V20 VERSIONS, WHICH DIFFER.
