5 REM LEMONADE STAND FOR X16, EDIT 2021 ROBERT EAGLESTONE
10 REM BORING NOTES AT THE END OF THIS LISTING
15 REM
20 REM TO USE THE PET FONT:
25 REM
30 VLOAD "PETFONT.BIN",8,1,0
35 POKE $9F36,128
40 DIM A0$(30)
45 A0$(0)="\X13"
50 FORX=1TO30:A0$(X)=A0$(X-1)+"\X11":NEXT
55 REM ------------------------------------
60 REM
65 REM
70 REM  COMMODORE TITLE PAGE (CA. 1986)
75 REM
80 REM
85 REM ------------------------------------
90 SCREEN 0  :REM X16 40X25 MODE
95 COLOR 5,0 :REM X16 GREEN-ON-BLACK
100 ?CHR$(147)
105 PRINT"\X93\X11\X1D";
110 FORI=1TO38:PRINT"\X12Š\X92";:NEXT
115 FORI=1TO27:PRINT"\X11\X9D\X12Š\X92";:NEXT
120 FORI=1TO37:PRINT"\X9D\X9D\X12Š\X92";:NEXT
125 FORI=1TO27:PRINT"\X91\X9D\X12Š\X92";:NEXT
130 REM ------------------------------------
135 REM
140 REM  PRINT THE COMMODORE LOGO!
145 REM
150 REM ------------------------------------
155 COLOR 6,0 :REM LTBLUE ON BLACK
160 PRINT"\X13\X11\X11\X11\X11\X11\X11\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12©  \X92
165 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12 \X92  \X12 \X92©
170 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12 \X92  ";
175 COLOR 2,0 :REM RED ON BLACK
180 PRINT "\X12£ß
185 COLOR 6,0 :REM LTBLUE ON BLACK
190 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1Dß\X12  \X92
195 COLOR 5,0 :REM X16 GREEN-ON-BLACK
200 PRINT"\X11\X11\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DC O M M O D O R E\X11\X11
205 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DEDUCATIONAL\X11\X11
210 PRINT"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1DSOFTWARE
215 ? A0$(24);"\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X12PRESS SPACE BAR OR WAIT\X92":I=0
220 TI$="000000"
225 GETA$:IFA$<>" "THENIFVAL(TI$)<7THEN 225
230 GOSUB 2125 :REM INTRO
235 V=RND(-TI) :REM SEED RANDOMIZER
240 ?"\X93\X11\X11 LEMONADE STAND \X11\X11"
245 INPUT" WHAT IS YOUR NAME";N$
250 IF N$="" GOTO 90 
255 N$=LEFT$(N$,13)
260 ?
265 REM ------------------------------------
270 REM
275 REM
280 REM  INITIALIZATION
285 REM
290 REM
295 REM ------------------------------------
300 WK=12 :REM # OF WEEKS TO RUN
305 B=10  :REM STARTING CASH
310 D1=0  :REM STARTING LEMON CONCENTRATE
315 A1=0  :REM STARTING SUGAR
320 P1=0  :REM STARTING CUPS
325 X=1
330 ?:?" " N$", YOU SHALL OPERATE YOUR"
335 ?:?" STAND FOR"WK"WEEKS."
340 GOSUB 1750 :REM SPACEBAR
345 REM ------------------------------------
350 REM
355 REM
360 REM  MAIN LOOP
365 REM
370 REM
375 REM ------------------------------------
380 FX=0
385 ? "\X93\X11";
390 ?" WEEK NUMBER" X
395 ? "
400 ? "                       YOU HAVE:
405 ? "
410 ? "    §£££££££££££££££¥         QTS/CONC
415 ? "    §               ¥         LBS/SUGAR
420 TA=4+INT(((14-LEN(N$))/2)+.5)
425 ? TAB(TA)"\X91"N$"'S"
430 ? "    §   LEMONADE    ¥         CUPS
435 ? "    §               ¥         
440 ? "    §        CENTS  ¥  WEATHER:
445 ? "    §£££££££££££££££¥  
450 ? "    § €€€           ¥  
455 ? "    § Í ÎÉ          ¥
460 ? "    § ( )Ë¡¡¡¡¡¡¡¡¡ ¥
465 ? "    §\X12©             ß\X92¥
470 ? "    \X12©               ß\X92
475 ? "    È(Ù@Ç()ÕÉÇ()Ç)Ç*Ç
480 ? "    È)Ù(Â)(ÊËÂ)(Â@Ç)Ç
485 ? "    È(Ù)Ç(×()Ç(*Ç)Ç(Ç
490 ? "    È)Ù(Â)(@(Â)(Â(Ç)Ç
495 ? "    È*Ù)Ç*)()Ç()Ç)Ç(Ç
500 ? "    È)Ù(Ç)()(Ç)(Ç(Ç)Ç
505 ? "±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
510 GOSUB 1645 :REM SHOW VALUES
515 REM ------------------------------------
520 REM
525 REM  PREDICTIONS
530 REM
535 REM  T: TEMP
540 REM  H: HUMIDITY
545 REM  S: COST OF SUGAR
550 REM  L: COST OF LEMON CONCENTRATE
555 REM  C: COST OF CUPS
560 REM  Z: TEMP VARIATION (ACTUAL)
565 REM  T2: FELT (LOW) TEMPERATURE W/HUMIDITY
570 REM  F2,F4: THRESHOLD VALUES
575 REM
580 REM ------------------------------------
585 T=(INT(RND(1)*3)+7)*10
590 H=RND(1)
595 IF H>.66 THEN H$="HIGH"
600 IF H<.33 THEN H$="LOW"
605 IF H>.32 AND H<.67 THEN H$="AVERAGE"
610 S=.99+(.01)*INT(RND(1)*9):REM**COST OF SUGAR
615 L=.69+(.01)*INT(RND(1)*9):REM**COST OF CONCENTRATE
620 C=.90+(.01)*INT(RND(1)*9):REM**COST OF CUPS
625 Z=-5+INT(RND(1)*20)
630 T2=T+Z-INT(H*10)
635 F2=INT(RND(1)*30)+70
640 F4=(((T+Z+T2)-137.5)/25)
645 IF F4<.1 THEN F4=.1
650 IF F4>1  THEN F4=1
655 GOSUB 1920 :REM CLEAR W
660 ? A0$(11) TAB(23) "PREDICTION FOR"
665 ? A0$(12) TAB(23) "SATURDAY:"
670 ? A0$(14) TAB(23) "TEMPERATURES IN"
675 ? A0$(15) TAB(23) "THE"T"\X9D'S AND"
680 ? A0$(16) TAB(23)  H$" HUMIDITY"
685 REM ------------------------------------
690 REM 
695 REM 
700 REM  BUY LEMON CONCENTRATE
705 REM 
710 REM 
715 REM ------------------------------------
720 GOSUB 1645 :REM SHOW VALUES
725 GOSUB 1975 :REM CLEAR D
730 ? A0$(23) "CONC. LEMON IS $"L"A QUART."
735 ? A0$(24) "HOW MANY QUARTS DO YOU WANT TO BUY? ";
740 GOSUB 2030 :D=AN 
745 IF D*L<B GOTO 770
750 ? "YOU DON'T HAVE ENOUGH MONEY"
755 ? "FOR "D" BOTTLES."
760 GOSUB 1750 :REM SPACEBAR
765 GOTO 720
770 B=B-(D*L) :D1=D1+D
775 GOSUB 1645 :REM SHOW VALUES
780 IF FX=2 GOTO 1030 :REM CHANGE
785 REM ------------------------------------
790 REM 
795 REM 
800 REM  BUY SUGAR
805 REM 
810 REM 
815 REM ------------------------------------
820 GOSUB 1645 :REM SHOW VALUES
825 GOSUB 1975 :REM CLEAR D
830 ? A0$(23) "SUGAR IS $"S"FOR A 5-LB BAG."
835 ? A0$(24) "HOW MANY BAGS DO YOU WANT TO BUY? ";
840 GOSUB 2030 :A=AN
845 IF A*S<B GOTO 870
850 ? A0$(25) "YOU DON'T HAVE ENOUGH MONEY"
855 ? A0$(26) "FOR" A "BAGS."
860 GOSUB 1750 :REM SPACEBAR
865 GOTO 820
870 B=B-(A*S) :A1=A1+(5*A)
875 GOSUB 1645 :REM SHOW VALUES
880 IF FX=2 GOTO 1030 :REM CHANGE
885 REM ------------------------------------
890 REM 
895 REM 
900 REM  BUY CUPS
905 REM 
910 REM 
915 REM ------------------------------------
920 GOSUB 1975 :REM CLEAR D
925 ? A0$(23) "CUPS ARE $"C"FOR A BAG OF 100."
930 ? A0$(24) "HOW MANY BAGS DO YOU WANT TO BUY? ";
935 GOSUB 2030 :P=AN
940 IF P*C<B GOTO 960
945 ? "YOU DON'T HAVE ENOUGH MONEY"
950 ? "FOR"P"BAGS."
955 GOTO 920
960 B=B-(P*C):P1=P1+(P*100)
965 GOSUB 1645 :REM SHOW VALUES
970 IF FX=2 GOTO 1030 :REM CHANGE
975 REM ------------------------------------
980 REM
985 REM
990 REM  CLEAR TEMPERATURE PREDICTION
995 REM  AND REPLACE WITH ACTUALS.
1000 REM
1005 REM
1010 REM ------------------------------------
1015 GOSUB 1920 :REM CLEAR W
1020 ? A0$(12) TAB(23) "TEMPERATURE:"Z+T
1025 ? A0$(13) TAB(23) "WET BULB   :"T2
1030 GOSUB 1975 :REM CLEAR D
1035 ? A0$(23) "DO YOU WANT TO CHANGE ANY"
1040 ? "OF THE ITEMS? [YN]";
1045 GET YN$: IF YN$="N" THEN 1100
1050 IF YN$<>"Y" GOTO 1045
1055 FX=2
1060 GOSUB 1975 :REM CLEAR D
1065 ? A0$(23) "WHICH ITEM: LEMON=1   SUGAR=2   CUPS=3"
1070 NI$=""
1075 GET NI$
1080 IF NI$="1" THEN B=B+(A*S) :D1=D1-D       :GOTO 720
1085 IF NI$="2" THEN B=B+(D*L) :A1=A1-(5*A)   :GOTO 820
1090 IF NI$="3" THEN B=B+(P*C) :P1=P1-(P*100) :GOTO 920
1095 GOTO 1075
1100 GOSUB 1975 :REM CLEAR D
1105 ? A0$(23) "PRICE (IN CENTS) FOR A CUP"
1110 ? "OF LEMONADE: ";
1115 GOSUB 2030 :K=AN
1120 IF K<1 OR K>80 GOTO 1100
1125 F3=3-((K/3)/((((T+Z+T2)-37.5)^3)/10^6))
1130 IF F3<0 THEN F3=0
1135 GOSUB 1975 :REM CLEAR D
1140 ? A0$(9) TAB(9) K :REM PRICE PER CUP
1145 F1=INT(F2*F3*F4)
1150 IF F1<0 THEN F1=0
1155 IF F1<1 THEN W=1: IF F1<1 GOTO 1325
1160 REM ------------------------------------
1165 REM 
1170 REM 
1175 REM  RUN THE SIMULATION F1 TIMES
1180 REM
1185 REM 
1190 REM ------------------------------------
1195 FOR W=1 TO F1
1200    IF P1<.01 THENP1=0
1205    IF D1<.01 THEND1=0
1210    IF A1<.01 THENA1=0
1215    IF P1=0 OR A1=0 OR D1=0 GOTO 1250
1220    GOSUB 1645 :REM SHOW VALUES
1225    D1=D1-(1/40)
1230    A1=A1-(1/20)
1235    P1=P1-1
1240    B=B+(K*.01)
1245 NEXT W
1250 GOSUB 1645 :REM SHOW VALUES
1255 REM ------------------------------------
1260 REM 
1265 REM 
1270 REM  DISPLAY RESULTS AND COMMENTARY
1275 REM
1280 REM 
1285 REM ------------------------------------
1290 IF W=F1 GOTO 1325
1295 MM$="CUPS."
1300 IF P1<=0 THEN GOSUB 1855
1305 MM$="LEMON JUICE."
1310 IF D1<=0 THEN GOSUB 1855
1315 MM$="SUGAR."
1320 IF A1<=0 THEN GOSUB 1855
1325 GOSUB 1975 :REM CLEAR D
1330 ? A0$(23) "YOU MADE"W-1"SALES AT"K"CENTS A CUP."
1335 ? A0$(24) "YOU GROSSED $"(W-1)*(K/100)"\X9D."
1340 FOR K1=1 TO 50
1345    F5=3-((K1/3)/((((T+Z+T2)-37.5)^3/10^6)))
1350    G5=(INT(F5*F2*F4))*(K1/100)
1355    IF G5<G9 GOTO 1370
1360    G9=G5
1365    K9=K1
1370 NEXTK1
1375 IF K9=K GOTO 1395 :REM BEST PRICE
1380 ? A0$(25) "YOU COULD HAVE GROSSED $"G9"TODAY,"
1385 ? A0$(26) "IF YOU HAD CHARGED"K9"CENTS."
1390 GOTO 1410
1395 ? A0$(25)  K "CENTS WAS THE BEST PRICE FOR TODAY."
1400 IF FX<>1 THEN ? A0$(26) "YOU'RE A GOOD BUSINESSPERSON."
1405 COUNT=COUNT+1
1410 K9=0:G9=0
1415 GOSUB 1800 :REM WAIT
1420 GOSUB 1750 :REM SPACEBAR
1425 X=X+1: IF X<=WK GOTO 380 :REM LOOP
1430 REM ------------------------------------
1435 REM 
1440 REM 
1445 REM  END / SUMMARY
1450 REM
1455 REM 
1460 REM ------------------------------------
1465 ?CHR$(147)
1470 ? "\X12SUMMER'S END\X92"
1475 ?:? " THE SUMMER IS OVER."
1480 ?:? " YOU HAVE $"B"."
1485 ?:? " YOU OWE YOUR FAMILY $10, SO"
1490 ?:? " YOU EARNED $"B-10"THIS SUMMER."
1495 ?:? " YOU HAVE"P1"CUPS,"INT(D1)"BOTTLES OF"
1500 ?:? " LEMON JUICE, AND"INT(A1)"LBS OF"
1505 ?:? " SUGAR LEFT OVER."
1510 IF COUNT<WK/2 GOTO 1545
1515 ?
1520 ?:? " " N$ ", YOU ARE TRULY A GIFTED BUSINESS-"
1525 ?:? " PERSON. "; INT(COUNT/(X-1)*100);"% OF YOUR PRICES"
1530 ?:? " WERE THE BEST POSSIBLE!! "
1535 ?:? " THE ROCKEFELLERS AND GETTYS OF THE"
1540 ?:? " WORLD SALUTE YOU."
1545 GOSUB 1750
1550 SCREEN $02 :REM X16 BACK TO 80X60
1555 ?:?:? "THANK YOU FOR PLAYING!"
1560 END
1565 REM ------------------------------------
1570 REM
1575 REM
1580 REM
1585 REM       SUBROUTINES BEGIN HERE
1590 REM
1595 REM
1600 REM
1605 REM ------------------------------------
1610 REM ------------------------------------
1615 REM
1620 REM
1625 REM  SHOW VALUES (OLD LINE 3130)
1630 REM
1635 REM
1640 REM ------------------------------------
1645 FORU=1TO6
1650    IF MID$(STR$(B),U,1)="."GOTO 1665
1655 NEXT U
1660 GOTO 1675
1665 B=VAL(LEFT$(STR$(B),U+2))
1670 B=INT(B*100)/100
1675 U6=ABS(B)+1
1680 U7=INT(U6)
1685 U6$=STR$(U7-1)+"."+MID$(STR$(U6)+"00000",LEN(STR$(U7))+2,2)
1690 ? A0$(4) TAB(23)"          \X9D\X9D\X9D\X9D\X9D\X9D\X9D$" U6$
1695 ? A0$(5) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" INT(D1)
1700 ? A0$(6) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" INT(A1)
1705 ? A0$(7) TAB(23)"       \X9D\X9D\X9D\X9D\X9D" P1
1710 RETURN
1715 REM ------------------------------------
1720 REM
1725 REM
1730 REM  PRESS SPACE TO CONTINUE
1735 REM 
1740 REM
1745 REM ------------------------------------
1750 ? A0$(27) "\X1D\X1D\X1D\X1D\X1D\X1D\X12 PRESS SPACEBAR TO CONTINUE "
1755 GETA$:IFA$<>" "THEN 1755
1760 RETURN
1765 REM ------------------------------------
1770 REM
1775 REM
1780 REM  A SHORT WAIT (200 JIFFYS)
1785 REM
1790 REM
1795 REM ------------------------------------
1800 TT=TI+200
1805 IF TT>TI GOTO 1805
1810 RETURN
1815 REM ------------------------------------
1820 REM
1825 REM
1830 REM  YOU WOULD HAVE MADE <F1> SALES, 
1835 REM           IF YOU HAD MORE <MM$>.
1840 REM
1845 REM
1850 REM ------------------------------------
1855 GOSUB 1975 :REM CLEAR D
1860 ? A0$(23) "YOU WOULD HAVE MADE"F1"SALES,"
1865 ? A0$(24) "IF YOU HAD MORE " MM$
1870 FX=1
1875 GOSUB 1800 :REM WAIT
1880 RETURN
1885 REM ------------------------------------
1890 REM
1895 REM
1900 REM  CLEAR WEATHERBOARD
1905 REM
1910 REM
1915 REM ------------------------------------
1920 FOR V=10 TO 18
1925 ? A0$(V) TAB(23) "                "
1930 NEXT V
1935 RETURN
1940 REM ------------------------------------
1945 REM
1950 REM
1955 REM  CLEAR DIRECTIVES
1960 REM
1965 REM
1970 REM ------------------------------------
1975 FOR V=23 TO 28
1980 ? A0$(V) "                                       "
1985 NEXT V
1990 RETURN
1995 REM ------------------------------------
2000 REM
2005 REM
2010 REM  DIGIT COLLECTION
2015 REM
2020 REM
2025 REM ------------------------------------
2030 GET A1$ :IF A1$<"0" OR A1$>"9" GOTO 2030
2035 ? A1$;
2040 GET A2$ :IF A2$="" GOTO 2040
2045 IF A2$=CHR$(20) THEN ? CHR$(20); :GOTO 2030
2050 IF A2$=CHR$(13) THEN A2$="" :GOTO 2080
2055 IF A2$<"0" OR A2$>"9" THEN 2040
2060 ? A2$;
2065 GET A3$ :IF A3$="" GOTO 2065
2070 IF A3$=CHR$(20) THEN ? CHR$(20); :GOTO 2040
2075 IF A3$<>CHR$(13) GOTO 2065
2080 AN=VAL(A1$+A2$)
2085 RETURN
2090 REM ------------------------------------
2095 REM
2100 REM
2105 REM  INTRODUCTION
2110 REM
2115 REM
2120 REM ------------------------------------
2125 ?CHR$(147);
2130 ? CHR$(14);
2135 ? " \X12LEMONADE STAND\X92"
2140 ?
2145 ? " YOU'VE DECIDED TO RUN A LEMONADE STAND"
2150 ? " FOR 12 SATURDAYS THIS SUMMER IN ORDER "
2155 ? " TO MAKE SOME MONEY. ÂUT YOU SOON FIND "
2160 ? " THAT RUNNING A STAND COSTS MONEY."
2165 ? " YOUR JOB IS TO MAKE AS BIG A PROFIT AS"
2170 ? " YOU CAN."
2175 ?
2180 ? " YOUR FAMILY AGREES TO STAKE YOU $10 IN"
2185 ? " ORDER TO GET STARTED. AT THE END OF "
2190 ? " THE SUMMER YOU WILL HAVE TO RETURN THE"
2195 ? " $10, BUT YOU WILL NOT NEED TO PAY"
2200 ? " INTEREST."
2205 ?
2210 ? " IF YOU KEEP YOUR PRICE LOW, YOU'LL SELL";
2215 ? " MORE LEMONADE, BUT WILL MAKE LESS MONEY";
2220 ? " ON EACH SALE."
2225 ? " ON HOT OR HUMID DAYS, YOU WILL SELL"
2230 ? " MORE LEMONADE AND ALSO BE ABLE TO"
2235 ? " CHARGE MORE PER CUP."
2240 ? " WHEN THE WET BULB READING IS CLOSE TO"
2245 ? " THE TEMPERATURE, IT IS A HUMID DAY."
2250 GOSUB 1800 :REM WAIT
2255 GOSUB 1750 :REM SPACEBAR
2260 ?CHR$(147):? CHR$(142)
2265 RETURN
2270 "-----------------------------------------------------
2275 :
2280 :
2285 "LEMONADE STAND DESIGN NOTES, R.EAGLESTONE 2021
2290 :
2295 :
2300 "-----------------------------------------------------
2305 "MY GOAL IS TO RE-CREATE THE ORIGINAL PET EXPERIENCE,
2310 "BUT ON THE X16 WITH A 40 X 25 DISPLAY.  I GO SO FAR AS 
2315 "TO USE THE ORIGINAL STARTING CASH AMOUNT, ORIGINAL 
2320 "IMPERIAL UNITS, AND A GREEN-ON-BLACK DISPLAY.
2325 :
2330 "THERE ARE A VERY FEW X16-SPECIFIC COMMANDS USED:
2335 "'COLOR' AND 'SCREEN' TO BE EXACT.
2340 :
2345 "WHAT IS UNCHANGED ARE THE ALGORITHMS USED TO 
2350 "CALCULATE THE PRICES, TEMPERATURES, AND DEMAND --
2355 "I.E. THE CORE MECHANICS OF THE GAME ITSELF.
2360 :
2365 "EFFORTS TO FIND THE SOURCE RETURNED TWO LATE VARIANTS:
2370 "(1) A VIC-20 LISTING MADE IN 2009
2375 "(2) THE C64 VERSION FROM 1986
2380 :
2385 "THE VIC-20 VERSION IS GREATLY STRIPPED DOWN, WHICH
2390 "ACTUALLY WAS A BOON SINCE IT LAID BARE THE FUNDAMENTAL
2395 "STRUCTURE OF THE GAME.
2400 :
2405 "THE C64 VERSION IS ABOUT 50% BOILERPLATE AND 
2410 "FRIVOLOUS POKES, INCLUDING SOME ML TO PREVENT THE
2415 "CODE FROM BEING MODIFIED.  AT THE SAME TIME, THAT
2420 "VERSION NOTES THAT THE PROGRAM WAS IN THE PUBLIC
2425 "DOMAIN. ITS CODE IS MORE FAITHFUL TO THE ORIGINAL,
2430 "AS WELL, ALTHOUGH DIFFERENCES EXIST.
2435 :
2440 "THIS VERSION WAS REBUILT USING THE VIC-20 VERSION
2445 "TO RECAPTURE THE ORIGINAL FLOW, THEN ADAPTING THE
2450 "1986 LOGIC INTO THE FRAMEWORK, PIECE BY PIECE.
2455 :
2460 "SUBROUTINES ARE NOT EQUIVALENT TO THE ORIGINALS.
2465 "IN FACT, EVEN THE INTERFACE IS NOT THE SAME, SINCE
2470 "I USED THE ORIGINAL PET VIEW AS THE TEMPLATE,
2475 "RATHER THAN THE C64/V20 VERSIONS, WHICH DIFFER.
